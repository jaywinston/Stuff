class Str {

    function String cat(String s, String t) {
        var String new;

        let new = String.new(s.length() + t.length());
        do Str.cpy(s, new);
        do Str.cpy(t, new);
        return new;
    }

    function void truncate(String s) {
        var int len;

        let len = s.length();
        while (len > 0) {
            do s.eraseLastChar();
            let len = len - 1;
        }

        return;
    }

    function void cpy(String s, String t) {
        var int i, sLen;

        let sLen = s.length();
        if (sLen > t.length()) {
            /* todo: abort */
            return;
        }

        let i = 0;
        while (i < sLen) {
            do t.appendChar(s.charAt(i));
            let i = i + 1;
        }

        return;
    }

    function boolean eq(String s, String t) {
        var int i;
        var int sLen, tLen;

        let sLen = s.length();
        let tLen = t.length();
        let i = 0;
        while ( (i<sLen) & (i<tLen) ) {
            if (~(s.charAt(i) = t.charAt(i))) {
                return false;
            }
            let i = i + 1;
        }

        if ( (i<sLen) | (i<tLen)) {
            return false;
        }

        return true;
    }

    function String dup(String s) {
        var int i, len;
        var String t;

        if (s = null) {
            return null;
        }

        let len = s.length();
        let t = String.new(len);
        let i = 0;
        while (i < len) {
            do t.appendChar(s.charAt(i));
            let i = i + 1;
        }

        return t;
    }

    function int tkn(String s, String t, String list, int i) {
        var char c;
        var int j;
        var int sLen, listLen;
        var boolean continue;

        let sLen = s.length();
        if (i = sLen) {
            return i;
        }
        let listLen = list.length();
        let continue = true;
        let c = s.charAt(i);
        let j = 0;
        while ( (j<listLen) & continue ) {
            let continue = ~(c = list.charAt(j));
            let j = j + 1;
        }
        let i = i + 1;

        while ( (i<sLen) & continue ) {
            do t.appendChar(c);
            let c = s.charAt(i);
            let j = 0;
            while ( (j<listLen) & continue ) {
                let continue = ~(c = list.charAt(j));
                let j = j + 1;
            }
            let i = i + 1;
        }

        if (continue) {
            do t.appendChar(c);
            return i;
        }
        return i - 1;
    }

    function void shift(String s, int off) {
        var int i, sLen;

        let sLen = s.length();
        let i = off;
        while (i < sLen) {
            do s.setCharAt(i-off, s.charAt(i));
            let i = i + 1;
        }

        while (off > 0) {
            do s.eraseLastChar();
            let off = off - 1;
        }

        return;
    }
}
