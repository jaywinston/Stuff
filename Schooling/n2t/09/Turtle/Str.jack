class Str {

    function boolean eq(String s, String t) {
        var int i;

        let i = 0;
        while ( (i<s.length()) & (i<t.length()) ) {
            if (~(s.charAt(i) = t.charAt(i))) {
                return false;
            }
            let i = i + 1;
        }

        if ( (i<s.length()) | (i<t.length())) {
            return false;
        }

        return true;
    }

    function String dup(String s) {
        var int i, len;
        var String t;

        let len = s.length();
        let t = String.new(len);
        let i = 0;
        while (i < len) {
            do t.appendChar(s.charAt(i));
            let i = i + 1;
        }

        return t;
    }

    /**
     * return characters from s, up to and including a delimiter from list
     */
    function String tkn(String s, String dummy, String list) {
        var int i, j;
        var boolean continue;
        var String t;

        let t = String.new(s.length());
        let continue = true;
        let i = 0;
        while ( (i<s.length()) & continue ) {
            let j = 0;
            while ( (j<list.length()) & continue ) {
                let continue = ~(s.charAt(i) = list.charAt(j));
                let j = j + 1;
            }
            do t.appendChar(s.charAt(i));
            let i = i + 1;
        }

        let j = 0;
        while ((j+i) < s.length()) {
            do s.setCharAt(j, s.charAt(j+i));
            let j = j + 1;
        }

        while (i > 0) {
            do s.eraseLastChar();
            let i = i - 1;
        }

        return t;
    }

}
