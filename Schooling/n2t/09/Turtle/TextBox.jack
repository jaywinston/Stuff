class TextBox {

    field int row, col;
    field int left, right, top, bottom;

    constructor TextBox new(int i, int j, int h, int w) {

        let left = j;
        let right = j + w;
        let top = i;
        let bottom = i + h;

        let row = top;
        let col = left;

        do clear();

        return this;
    }

    method void dispose() {
        do Memory.dealloc(this);
        return;
    }

    method void clear() {
        var int x1, y1, x2, y2;

        if (left > 0) {
            let x1 = (left*8) - 2;
        } else {
            let x1 = 0;
        }

        if (top > 0) {
            let y1 = (top*11) - 2;
        } else {
            let y1 = 0;
        }

        if (right < 64) {
            let x2 = (right*8);
        } else {
            let x2 = 510;
        }

        if (bottom < 23) {
            let y2 = (bottom*11) + 1;
        } else {
            let y2 = 255;
        }


        do Screen.setColor(true);
        do Screen.drawRectangle(x1, y1, x2, y2);

        if (x1 > 0) {
            let x1 = x1 + 1;
        }
        if (y1 > 0) {
            let y1 = y1 + 1;
        }
        if (x2 < 510) {
            let x2 = x2 - 1;
        }
        if (y2 < 255) {
            let y2 = y2 - 1;
        }

        do Screen.setColor(false);
        do Screen.drawRectangle(x1, y1, x2, y2);

        return;
    }

    method void reset() {
        do clear();
        let row = top;
        let col = left;
        return;
    }

    method void newline() {
        let col = left;
        let row = row + 1;

        if (row = bottom) {
            do scroll();
            let row = row - 1;
        }

        return;
    }

    method void scroll() {
        var boolean odd;
        var int w, end, off, pxRow;
        var int oddChar;

        let odd = -(w & 1);
        let w = (right-left) / 2;
        let end = 16384 + (((bottom-1)*352) + (left/2));
        let pxRow = 16384 + ((top*352) + (left/2));

        while (pxRow < end) {
            let off = 0;
            while (off < w) {
                do Memory.poke(pxRow+off, Memory.peek((pxRow+off)+352));
                let off = off + 1;
            }
            if (odd) {
                let oddChar = Memory.peek((pxRow+off)+352) & 255;
                let oddChar = oddChar | (Memory.peek(pxRow+off) & -256);
                do Memory.poke(pxRow+off, oddChar);
            }
            let pxRow = pxRow + 32;
        }

        do Output.moveCursor(bottom-1, left);

        let w = left;  // recycle 'w'; nothing to do with it's name

        while (w < right) {
            do Output.printChar(32);
            let w = w + 1;
        }

        do Screen.setColor(true);
        do Screen.drawLine((left*8)-2, top*11, (left*8)-2, bottom*11);

        return;
    }

    method void backspace() {
        if ( (col=left) & (row>top) ) {
            let col = right - 1;
            let row = row - 1;
            if (left > 0)  {
                do Screen.setColor(true);
                do Screen.drawLine((left*8)-2, top*11, (left*8)-2, bottom*11);
            }
        } else {
            let col = col - 1;
        }

        return;
    }

    method void putc(int c) {

        if (col = right) {
            do newline();
            /* The vm emulator insists on an extra column to the right. */
            if (right < 64)  {
                do Screen.setColor(true);
                do Screen.drawLine(right*8, top*11, right*8, bottom*11);
            }
        }

        if (c = 128) {
            do newline();
            return;
        } else { if (c = 129) {
            do backspace();
            return;
        }}

        do Output.moveCursor(row, col);
        do Output.printChar(c);

        let col = col + 1;

        return;
    }

    method void puts(String s) {
        var int i, len;

        let len = s.length();
        let i = 0;
        while (i < len) {
            do putc(s.charAt(i));
            let i = i + 1;
        }

        return;
    }

    method void prints(String s) {
        var int i;

        do Output.moveCursor(row, col);
        let i = col;
        while (i < right) {
            do Output.printChar(32);
            let i = i + 1;
        }

        do puts(s);
        do newline();

        return;
    }

}
