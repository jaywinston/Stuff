class Terminal {

    static int BUFSIZ;
    static int bp;
    static Array linbuf;
    static TextBox tb;

    function void init(int i, int j, int h, int w) {

        let BUFSIZ = 23;
        let linbuf = Array.new(BUFSIZ);
        let bp = 0;
        while (bp < BUFSIZ) {
            let linbuf[bp] = String.new(Str.MAXLEN());
            let bp = bp + 1;
        }
        let bp = 0;
        let tb = TextBox.new(i, j, h, w);
        return;
    }

    function void dispose() {
        var int i;
        var String s;

        let i = 0;
        while (i < BUFSIZ) {
            let s = linbuf[i];
            do s.dispose();
            let i = i + 1;
        }
        do linbuf.dispose();
        do tb.dispose();
        return;
    }

    function String read(String ps) {
        var char c;
        var int recall;
        var String s;

        do tb.puts(ps);

        let s = linbuf[bp];
        do s.dispose();
        let linbuf[bp] = String.new(Str.MAXLEN());
        let s = linbuf[bp];

        let c = 0;
        while (~(c = 128)) {
            do tb.putc(0);      // show cursor
            do tb.backspace();  // next char overwrite cursor

            let c = Keyboard.keyPressed();
            while (c = 0) {
                let c = Keyboard.keyPressed();
            }
            do Memory.poke(24576, 0);

            if ( (c>31) & (c<127) ) {
                if (s.length() < Str.MAXLEN()) {
                    do s.appendChar(c);
                    do tb.putc(c);
                }
            } else { if ( (c=129) & (s.length()>0) ) {
                do s.eraseLastChar();
                do tb.putc(c);
            } else { if (c = 131) {
                /* recall */
            } else { if (c = 133) {
                /* recall */
            }}}}
        }

        let bp = bp + 1;
        if (bp = BUFSIZ) {
            let bp = 0;
        }

        do Output.printChar(129);   // erase cursor
        do tb.newline();

        return s;
    }

    function void print(String s) {
        do tb.puts(s);
        return;
    }

    function void echo(String s) {
        do tb.prints(s);
        return;
    }
}
