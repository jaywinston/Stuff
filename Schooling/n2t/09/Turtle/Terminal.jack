class Terminal {

    field int BUFSIZ, SCRSIZ;
    field int bp, scrtop;
    field Array LINBUF;
    field TextBox tb;

    constructor Terminal new(int i, int j, int h, int w) {

        let BUFSIZ = 100;
        let SCRSIZ = h;
        let LINBUF = Array.new(BUFSIZ);
        let bp = 0;
        while (bp < BUFSIZ) {
            let LINBUF[bp] = String.new(lib.MAXLEN());
            let bp = bp + 1;
        }
        let bp = 0;
        let tb = TextBox.new(i, j, h, w);
        return this;
    }

    method String read() {
        var char c;
        var String s;

        let s = LINBUF[bp];
        while (s.length() > 0) {
            do s.eraseLastChar();
        }

        let c = Keyboard.keyPressed();
        while (~(c = 128)) {
            if ( (c>31) & (c<127) ) {
                do s.appendChar(c);
                do tb.putc(c);
            } else { if ( (c=129) & (s.length()>0) ) {
                do s.eraseLastChar();
                do tb.putc(c);
            }}

            do tb.putc(0);      // show cursor
            do tb.backspace();  // next char overwrite cursor

            let c = Keyboard.keyPressed();
            while (c = 0) {
                let c = Keyboard.keyPressed();
            }
            do Memory.poke(24576, 0);
        }

        let bp = bp + 1;
        if (bp = BUFSIZ) {
            let bp = 0;
        }

        do Output.printChar(129);   // erase cursor
        do tb.newline();

        return s;
    }

    method void print(String s) {
        do tb.puts(s);
        return;
    }
}
