class Terminal {

    field int BUFSIZ;
    field int bp, scrtop;
    field Array linbuf;
    field String ESFULL;
    field TextBox tb;

    constructor Terminal new(int i, int j, int h, int w) {

        let BUFSIZ = 100;
        let linbuf = Array.new(BUFSIZ);
        let ESFULL = "buffer full";
        let bp = 0;
        while (bp < BUFSIZ) {
            let linbuf[bp] = String.new(lib.MAXLEN());
            let bp = bp + 1;
        }
        let bp = 0;
        let tb = TextBox.new(i, j, h, w);
        return this;
    }

    method void dispose() {
        do linbuf.dispose();
        do ESFULL.dispose();
        do tb.dispose();
        do Memory.dealloc(this);
        return;
    }

    method String read() {
        var char c;
        var String s;

        let s = linbuf[bp];
        while (s.length() > 0) {
            do s.eraseLastChar();
        }

        let c = Keyboard.keyPressed();
        while (~(c = 128)) {
            if ( (c>31) & (c<127) ) {
                if (s.length() = lib.MAXLEN()) {
                    do tb.newline();
                    do tb.prints(ESFULL);
                } else {
                    do s.appendChar(c);
                    do tb.putc(c);
                }
            } else { if ( (c=129) & (s.length()>0) ) {
                do s.eraseLastChar();
                do tb.putc(c);
            }}

            do tb.putc(0);      // show cursor
            do tb.backspace();  // next char overwrite cursor

            let c = Keyboard.keyPressed();
            while (c = 0) {
                let c = Keyboard.keyPressed();
            }
            do Memory.poke(24576, 0);
        }

        let bp = bp + 1;
        if (bp = BUFSIZ) {
            let bp = 0;
        }

        do Output.printChar(129);   // erase cursor
        do tb.newline();

        return s;
    }

    method void print(String s) {
        do tb.puts(s);
        return;
    }
}
