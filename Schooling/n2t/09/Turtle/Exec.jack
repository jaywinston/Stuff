class Exec {

    static int bottom, left, right, top;
    static TextBox tb;
    static Turtle t;

    static int
        NULL,
        BLK,
        CMD,
        CMDLIN,
        FUNC,
        INT,
        STR,
        SYM,

        ASSGN,
        BLOCK,
        CLEAR,
        ELSE,
        FORWARD,
        FUNCTION,
        IF,
        PENDOWN,
        PENUP,
        PRINT,
        REPEAT,
        TURNLEFT,
        TURNRIGHT,
        QUIT,
        _ECHO;


    function void init(int i, int j, int h, int w) {

        do Exec.initKWs();
        let tb = TextBox.new(i, j, h, w);

        let top = i;
        let left = j;
        let bottom = i + h;
        let right = j + w;

        do Exec.getTurtle();

        return;
    }


    /* intialize key words and internal symbols */
    function void initKWs() {
        var int i;

        let NULL   = Symbol.NULL();
        let BLK    = Symbol.BLK();
        let CMD    = Symbol.CMD();
        let CMDLIN = Symbol.CMDLIN();
        let FUNC   = Symbol.FUNC();
        let INT    = Symbol.INT();
        let STR    = Symbol.STR();
        let SYM    = Symbol.SYM();

        let ASSGN     =  0;
        let BLOCK     =  1;
        let CLEAR     =  2;
        let ELSE      =  3;
        let FORWARD   =  4;
        let FUNCTION  =  5;
        let IF        =  6;
        let PENDOWN   =  7;
        let PENUP     =  8;
        let PRINT     =  9;
        let REPEAT    = 10;
        let TURNLEFT  = 11;
        let TURNRIGHT = 12;
        let QUIT      = 13;

        do Eval.installKW("MV",128);  // ##DB##

        do Eval.installKW("=",         ASSGN);
        do Eval.installKW("CLEAR",     CLEAR);
        do Eval.installKW("ELSE",      ELSE);
        do Eval.installKW("FORWARD",   FORWARD);
        do Eval.installKW("FUNCTION",  FUNCTION);
        do Eval.installKW("IF",        IF);
        do Eval.installKW("PENDOWN",   PENDOWN);
        do Eval.installKW("PENUP",     PENUP);
        do Eval.installKW("PRINT",     PRINT);
        do Eval.installKW("REPEAT",    REPEAT);
        do Eval.installKW("TURNLEFT",  TURNLEFT);
        do Eval.installKW("TURNRIGHT", TURNRIGHT);
        do Eval.installKW("QUIT",      QUIT);

        return;
    }


    function void clear() {
        do t.dispose();
        do tb.clear();
        do Exec.getTurtle();
        return;
    }


    function void getTurtle() {
        let t = Turtle.new(((2*left) + (right-left)) * 4,
                          ((2*bottom) + (top-bottom)) * 5,
                          90, top, left, bottom, right);
        return;
    }


    /* execute a command line */
    function Symbol _exec(Symbol argv) {
        var int cmd;
        //var Symbol argv, retval;

        if (argv = null) {
            return null;
        }

        if (argv.type() = NULL) {
            do Error.send("unrecognized token"); // ##DB##
            do Error.set(1);  // unrecognized token
            do Lib.clearSym(argv);
            return null;
        }

        if (argv.type() = FUNC) {
            return Exec.call(argv);
        }

        return null;
        //return Exec.execute(argv);
    }

    //function Symbol execute(Symbol argv) {
    function Symbol exec(Symbol argv) {
        var int cmd;

        if (argv = null) {
            return null;
        }

        if (argv.type() = FUNC)   { return Exec.call(argv);              }
        if (argv.type() = BLOCK)  { return Exec.execBlock(argv.value()); }
        //if (argv.type() = CMDLIN) { do Exec.execute(argv.value());       }
        if (argv.type() = CMDLIN) { do Exec.exec(argv.value());       }

        if (~(argv.type() = CMD)) {
            return argv;
        }

        let cmd = argv.value();

        if (cmd = ASSGN)            { do Exec.doAssign(argv.next());    }
        else { if (cmd = CLEAR)     { do Exec.doClear(argv.next());     }
        else { if (cmd = FORWARD)   { do Exec.doForward(argv.next());   }
        else { if (cmd = FUNCTION)  { do Exec.doFunction(argv.next());  }
        else { if (cmd = IF)        { do Exec.doIf(argv.next());        }
        else { if (cmd = PENDOWN)   { do Exec.doPendown(argv.next());   }
        else { if (cmd = PENUP)     { do Exec.doPenup(argv.next());     }
        else { if (cmd = PRINT)     { do Exec.doPrint(argv.next());     }
        else { if (cmd = REPEAT)    { do Exec.doRepeat(argv.next());    }
        else { if (cmd = TURNLEFT)  { do Exec.doTurnleft(argv.next());  }
        else { if (cmd = TURNRIGHT) { do Exec.doTurnright(argv.next()); }
        else { if (cmd = QUIT)      { do Exec.doQuit(argv.next());      }
        }}}}}}}}}}}

        if (cmd=128) {do DB.mv();}  // ##DB##

        do Lib.clearSym(argv);

        return null;
    }

    function Symbol call(Symbol argv) {
        var int n, type;
        var Array farg;
        var String arg, fpres;
        var Symbol p;

        let farg = Array.new(1);
        let fpres = "$%d";
        let n = 1;
        let p = argv.next();
        while (~(p = null)) {
            let farg[0] = n;
            let arg = Str.format(fpres, farg);
            do Eval.install(arg, p.type(), p.value());
            do arg.dispose();
            let n = n + 1;
            let p = p.next();
        }

        let p = Exec.exec(Lib.dupSym(argv.value()));
        //let p = Exec.execute(Lib.dupSym(argv.value()));

        do farg.dispose();
        do fpres.dispose();

        return p;
    }


    function Symbol execBlock(Symbol b) {
        var Symbol p;

        let p = Exec.doBlock(b);
        do Lib.clearSym(b);
        return p;
    }


    function Symbol doBlock(Symbol b) {

        while (~(b.next() = null)) {
            do Lib.clearSym(Exec.exec(b.value()));
            //do Lib.clearSym(Exec.execute(b.value()));
            let b = b.next();
        }

        return Exec.exec(b.value());
        //return Exec.execute(b.value());
    }


    function Symbol doAssign(Symbol arg) {
        do Eval.assign(arg.value());
        return arg.next();
    }


    function Symbol doClear(Symbol arg) {
        do Exec.clear();
        return arg.next();
    }


    function Symbol doForward(Symbol arg) {

        if (arg.type() = BLOCK) {
            let arg = Exec.execBlock(arg);
        }

        do t.forward(arg.value());

        return arg.next();
    }


    function Symbol doFunction(Symbol arg) {

        if (arg = null) {
            do Error.set(1);  // no function name
            return arg.next();
        }

        do Eval.install(arg.name(), FUNC, arg.next());

        return arg.next();
    }


    function Symbol doIf(Symbol arg) {

        if (~(arg.value() = 0)) {
            do Lib.clearSym(Exec.exec(arg.next()));
            //do Lib.clearSym(Exec.execute(arg.next()));
        }

        return arg.next();
    }

    function Symbol doPendown(Symbol arg) {
        do t.pendown();
        return arg;
    }


    function Symbol doPenup(Symbol arg) {
        do t.penup();
        return arg;
    }


    function Symbol doPrint(Symbol arg) {
        return arg.next();
    }


    function Symbol doRepeat(Symbol arg) {
        var int i;

        if ( (arg.type()=CMD) & (arg.value()=IF) ) {
            return Exec.doRepeatIf(arg.next());
        }

        let i = arg.value();
        while (i > 0) {
            do Exec.try(arg.next());
            let i = i - 1;
        }

        let arg = arg.next();

        return arg.next();
    }


    function Symbol doRepeatIf(Symbol arg) {
        var int condition;
        var Symbol p;

        let condition = arg.value();
        while (~(condition = 0)) {
            do Exec.try(Lib.dupSym(arg.next()));
            let p = Eval.eval(arg.name(), 0);
            let condition = p.value();
            do Lib.clearSym(p);
        }

        return arg.next();
    }


    function Symbol doTurnleft(Symbol arg) {
        do t.turnleft(arg.value());
        return arg.next();
    }


    function Symbol doTurnright(Symbol arg) {
        do t.turnright(arg.value());
        return arg.next();
    }


    function void doQuit(Symbol arg) {
        do Sys.halt();
        return;
    }


    function void try(Symbol argv) {
        var Symbol retval;

        let retval = Exec.exec(argv);

        if (Error.isSet()) {
            do Terminal.echo(Error.message());
            if (Error.isSet()) {
                do Terminal.echo(Error.message());
            }
        } else {
            do Exec.echo(retval);
        }

        do Lib.clearSym(retval);

        return;
    }


    function void echo(Symbol sym) {
        while (~(sym = null)) {
            if (sym.type() = STR) {
                do Terminal.echo(sym.value());
            } else { if (sym.type() = SYM) {
                do Exec.echo(sym.value());
            } else { if (sym.type() = INT) {
                do Exec.echoInt(sym);
            }}}
            let sym = sym.next();
        }
        return;
    }


    function void echoInt(Symbol sym) {
        var String s;

        let s = String.new(6);
        do s.setInt(sym.value());
        do Terminal.echo(s);
        do s.dispose();
        return;
    }

}
