// Site use of the "meta" namespace is bit[0] to classify executables.

class Exec {

    static int BIN, BLOCK, CMD, CMDLIN, EXE, FUNC, INT, LIST, SITE, STR, NAME;
    static int ASSIGN, FUNCTION, IF, REPEAT, QUIT;
    static String IFS, ELSES;


    function void init() {

        let IFS = "IF";
        let ELSES = "ELSE";

        let BIN = LibSym.BIN();
        let BLOCK = LibSym.BLOCK();
        let CMD = LibSym.CMD();
        let CMDLIN = LibSym.CMDLIN();
        let EXE = 1;  /* magic number! */
        let FUNC = LibSym.FUNC();
        let INT = LibSym.INT();
        let LIST = LibSym.LIST();
        let SITE = ~LibSym.SITE() | EXE;
        let STR = LibSym.STR();
        let NAME = LibSym.NAME();

        let ASSIGN   = 1;
        let FUNCTION = 2;
        let IF       = 3;
        let REPEAT   = 4;
        let QUIT     = 5;

do Eval.install("MV",CMD,128);  // ##DB##

        do Eval.install("ASSIGN", CMD, ASSIGN);
        do Eval.install("FUNCTION", CMD, FUNCTION);
        do Eval.install("IF", CMD, IF);
        do Eval.install("REPEAT", CMD, REPEAT);
        do Eval.install("QUIT", CMD, QUIT);

        return;
    }


    function void kill() {
        do IFS.dispose();
        do ELSES.dispose();
        return;
    }


    function Symbol cmdlin(Collection argv) {
        var Symbol arg;

        if (argv = null) {
            return null;
        }

        let arg = Eval.arg(argv.pop());

        if (arg = null) {
            return null;
        }

        if ( (arg.meta()&EXE) = EXE ) {
            return Exec.executable(arg, argv);
        }

        return arg;
    }


    function Symbol executable(Symbol arg, Collection argv) {
        var int type;
        
        let type = arg.meta();

        if (type = BIN)  { return Bin.exec(arg, argv);  }
        if (type = CMD)  { return Exec.cmd(arg, argv);  }
        if (type = FUNC) { return Exec.call(arg, argv); }

        return null;
    }


    function Symbol cmd(Symbol arg, Collection argv) {
        var int cmd;

        let cmd = arg.data();

if (cmd=128) {return arg;}  // ##DB##

        do arg.dispose();

        if (cmd = ASSIGN)   { return Exec.doAssign(argv);   }
        if (cmd = FUNCTION) { return Exec.doFunction(argv); }
        if (cmd = IF)       { return Exec.doIf(argv);       }
        if (cmd = REPEAT)   { return Exec.doRepeat(argv);   }
        if (cmd = QUIT)     { return Exec.doQuit(argv);     }

        return null;
    }


    /* 'func' is passed as 'int'; here it's used as 'Symbol'
     */
    function Symbol call(Symbol func, Collection argv) {
        var Symbol ret;

        do Eval.pushContext(func, argv);
        let ret = Exec.cmdlin(func.data());
        do Eval.dropContext();
        do LibSym.destroySym(func);

        return ret;
    }


    function Symbol block(Collection cmdlist) {
        var Symbol cmdlin, ret;

        if (cmdlist = null) {  // todo: might not need
            return null;
        }

        while (cmdlist.length() > 1) {
            let cmdlin = cmdlist.pop();
            do Main.execute(cmdlin.data());
            do cmdlin.dispose();
        }

        let cmdlin = cmdlist.pop();
        do cmdlist.dispose();

        if (cmdlin = null) {  // todo: might not need
            return null;
        }

        let cmdlist = cmdlin.data();
        do cmdlin.dispose();
        let ret = Exec.cmdlin(cmdlin.data());

        while (cmdlist.length() > 0) {
            if (LibIO.noerr()) {
                do Main.enqueue(ret);
            } else {
                do LibSym.destroyList(cmdlist);
                let cmdlist = Collection.new();
            }
            do LibSym.destroySym(ret);
            let ret = Exec.cmdlin(cmdlin.data());
        }

        do cmdlist.dispose();

        return ret;
    }


    function void doAssign(Collection argv) {
        var String name;
        var Symbol arg;

        let arg = argv.pop();

        if (arg = null) {
            do LibIO.error("expected lvalue; need a better name for this");
            return;
        }

        if (~(arg.meta() = NAME)) {
            do LibIO.error("won't assign to that");
            return;
        }

        let name = arg.data();
        do arg.dispose();

        let arg = Eval.arg(argv.pop());
        if (arg = null) {
            do LibIO.error("expected expression");
            return;
        }

        do Eval.install(name, arg.meta(), arg.data());
        do arg.dispose();

        return;
    }


    function void doFunction(Collection argv) {
        var String name;
        var Symbol arg;

        let arg = argv.pop();

        if (arg = null) {
            do LibIO.error("no function name found");
            return;
        }

        if (~(arg.meta() = NAME)) {
            do LibIO.error("no function name found");
            return;
        }

        let name = arg.data();
        do arg.dispose();
        do Eval.install(name, FUNC, LibSym.copyList(argv));
        do name.dispose();
        do LibSym.clearList(argv);

        return;
    }


    function void doIf(Collection argv) {
        var Collection elsev;
        var Symbol arg;

        let arg = Eval.arg(argv.pop());
        let elsev = Exec.elsev(argv);

        if (arg.data() = 0) {
            if (~(elsev = null)) {
                do Main.execute(elsev);
            }
        } else {
            do Main.execute(argv);
        }

        do LibSym.destroySym(arg);
        do LibSym.destroyList(argv);
        do LibSym.destroyList(elsev);

        return;
    }


    function Collection elsev(Collection argv) {
        var int a;
        var int nif;
        var Symbol arg;

        let nif = 0;

        let a = 0;
        while (a < argv.length()) {
            let arg = argv.nth(a);
            if (arg.meta() = NAME) {
                if (Str.eq(ELSES, arg.data())) {
                    if (nif = 0) {
                        return Exec.elseVector(argv, a);
                    } else { if (nif < 0) {
                        do LibIO.error("extra 'ELSE' (todo: a better message)");
                        return null;
                    } else {
                        let nif = nif - 1;
                    }}
                } else { if (Str.eq(IFS, arg.data())) {
                    let nif = nif + 1;
                }}
            }
            let a = a + 1;
        }

        return null;
    }


    /* Collection.slice() would be easier to read but redundant to execute. */
    function Collection elseVector(Collection argv, int a) {
        var Collection elsev;

        let elsev = Collection.new();
        while ( argv.length() > (a+1) ) {
            do elsev.append(argv.remove(a+1));
        }

        do LibSym.destroySym(argv.remove(a));

        return elsev;
    }


    function void doRepeat(Collection argv) {
        var int i;
        var Symbol arg;

        let arg = Eval.arg(argv.pop());

        if (arg = null) {
            /* todo: abort */
            return;
        }

        if ( (arg.meta()=CMD) & (arg.data()=IF) ) {
            do arg.dispose();
            do Exec.doRepeatIf(argv);
            return;
        }

        /* todo: type check */

        let i = arg.data();
        do arg.dispose();
        while (i > 0) {
            do Main.execute(LibSym.copyList(argv));
            let i = i - 1;
        }

        do LibSym.destroyList(argv);

        return;
    }


    function void doRepeatIf(Collection argv) {
        var int condition;
        var Symbol arg;

        let arg = argv.pop();

        let condition = Eval.condition(arg);
        while (~(condition = 0)) {
            do Main.execute(LibSym.copyList(argv));
            let condition = Eval.condition(arg);
        }

        do LibSym.destroyList(argv);
        do LibSym.destroySym(arg);

        return;
    }


    function void doQuit(Symbol arg) {
        do Main.stop();  /* syscall! */
        return;
    }

}
