// Site use of the "meta" namespace is bit[0] to classify executables.

class Exec {

    static int BIN, BLOCK, CMD, CMDLIN, EXE, FUNC, INT, LIST, SITE, STR, NAME,
        INDEX, LSP;
    static int ASSIGN, FUNCTION, IF, LENGTH, OVER, REPEAT, QUIT;
    static String IFS, ELSES;


    function void init() {

        let IFS = "IF";
        let ELSES = "ELSE";

        let BIN = LibSym.BIN();
        let BLOCK = LibSym.BLOCK();
        let CMD = LibSym.CMD();
        let CMDLIN = LibSym.CMDLIN();
        let EXE = 1;  /* magic number! */
        let FUNC = LibSym.FUNC();
        let INDEX = LibSym.INDEX();
        let INT = LibSym.INT();
        let LIST = LibSym.LIST();
        let LSP = LibSym.LSP();
        let NAME = LibSym.NAME();
        let SITE = ~LibSym.SITE() | EXE;
        let STR = LibSym.STR();

        let ASSIGN   = 0;
        let IF       = 1;
        let LENGTH   = 2;
        let FUNCTION = 3;
        let OVER     = 4;
        let REPEAT   = 5;
        let QUIT     = 6;

do Eval.install("MC",CMD,128);  // ##DB##

        do Eval.install("ASSIGN", CMD, ASSIGN);
        do Eval.install("FUNCTION", CMD, FUNCTION);
        do Eval.install("IF", CMD, IF);
        do Eval.install("LENGTH", CMD, LENGTH);
        do Eval.install("REPEAT", CMD, REPEAT);
        do Eval.install("QUIT", CMD, QUIT);

        return;
    }


    function void kill() {
        do IFS.dispose();
        do ELSES.dispose();
        return;
    }


    function Symbol cmdlin(Collection argv) {
        var Symbol arg;

        let arg = Eval.arg(argv.pop());

        if ( (arg.meta()&EXE) = EXE ) {
            return Exec.executable(arg, argv);
        }

        return arg;
    }


    function Symbol executable(Symbol arg, Collection argv) {
        var int cmd, type;

        let type = arg.meta();

        if (type = FUNC) {
            return Exec.call(arg, argv);
        }

        let cmd = arg.data();

if (cmd=128) {return arg;}  // ##DB##

        do arg.dispose();

        if (type = BIN) { return Bin.exec(cmd, argv);     }
        if (type = CMD) { return Exec.cmd(cmd, argv);     }
        if (type = LSP) { return List.process(cmd, argv); }

        return null;
    }


    function Symbol cmd(int cmd, Collection argv) {

        if (cmd = ASSIGN)   { return Exec.doAssign(argv);   }
        if (cmd = FUNCTION) { return Exec.doFunction(argv); }
        if (cmd = IF)       { return Exec.doIf(argv);       }
        if (cmd = LENGTH)   { return Exec.doLength(argv);   }
        if (cmd = REPEAT)   { return Exec.doRepeat(argv);   }
        if (cmd = QUIT)     { return Exec.doQuit(argv);     }

        return null;
    }


    function Symbol call(Symbol func, Collection argv) {
        var Collection cpy;
        var Symbol ret;

        do Eval.pushContext(func, argv);

        let cpy = LibSym.copyList(func.data());
        let ret = Main.try(cpy);
        while (cpy.length() > 0) {
            do LibSym.destroySym(ret);
            let ret = Main.try(cpy);
        }

        do Eval.dropContext();
        do func.dispose();
        do cpy.dispose();

        return ret;
    }


    function Symbol block(Collection cmdlist) {
        var Symbol cmdlin, ret;

        if (cmdlist = null) {  // todo: no need?
            return null;
        }

        while (cmdlist.length() > 1) {
            let cmdlin = cmdlist.pop();
            do Main.try(cmdlin.data());
            do cmdlin.dispose();
        }

        let cmdlin = cmdlist.pop();
        do cmdlist.dispose();

        if (cmdlin = null) {  // todo: no need?
            return null;
        }

        let cmdlist = cmdlin.data();
        do cmdlin.dispose();
        let ret = Exec.cmdlin(cmdlist);

        while (cmdlist.length() > 0) {
            if (LibIO.noerr()) {
                do Main.enqueue(ret);
                do LibSym.destroySym(ret);
                let ret = Exec.cmdlin(cmdlist);
            } else {
                do LibSym.clearList(cmdlist);
                do LibSym.destroySym(ret);
                let ret = null;
            }
        }

        do cmdlist.dispose();

        return ret;
    }


    function void doAssign(Collection argv) {
        var String name;
        var Symbol arg;

        let arg = argv.pop();

        if (arg = null) {
            do LibIO.error("expected ref");
            return;
        }

        if ( (arg.meta()&(INDEX|NAME)) = 0 ) {
            do LibIO.error("won't assign to that");
            return;
        }

        if (arg.meta() = INDEX) {
            do Exec.assignIndex(arg, argv);
            return;
        }

        let name = arg.data();
        do arg.dispose();

        let arg = Eval.arg(argv.pop());

        if (arg = null) {
            do LibIO.error("expected expression");
            return;
        }

        do Eval.install(name, arg.meta(), arg.data());

        do arg.dispose();

        return;
    }


    function void assignIndex(Symbol arg, Collection argv) {
        var String name;
        var Collection indexv;
        var Symbol namep, indexp, valp;

        let indexv = arg.data();
        do arg.dispose();

        let namep = indexv.pop();
        let name = namep.data();
        do Eval.arg(namep);

        if (~(namep.meta() = LIST)) {
            do LibIO.error("expected list");
            do LibSym.destroyList(indexv);
            do LibSym.destroySym(namep);
            do LibSym.clearList(argv);
            do name.dispose();
            return;
        }

        let indexp = Eval.arg(indexv.pop());
        do indexv.dispose();

        if (~(indexp.meta() = INT)) {
            do LibIO.error("index must be integer");
            do LibSym.destroySym(indexp);
            do LibSym.destroySym(namep);
            do LibSym.clearList(argv);
            do name.dispose();
            return;
        }

        let indexv = namep.data();

        let valp = Eval.arg(argv.pop());

        if (valp = null) {
            do LibIO.error("expected expression");
            do LibSym.destroySym(indexp);
            do LibSym.destroySym(namep);
            do LibSym.destroySym(valp);
            do LibSym.clearList(argv);
            do name.dispose();
            return;
        }

        do LibSym.destroySym(indexv.nth(indexp.data()));
        do indexv.setnth(indexp.data(), valp);

        do indexp.dispose();

        return;
    }


    function void doFunction(Collection argv) {
        var String name;
        var Symbol arg;

        let arg = argv.pop();

        if (arg = null) {
            do LibIO.error("no function name found");
            return;
        }

        if (~(arg.meta() = NAME)) {
            do LibIO.error("no function name found");
            return;
        }

        let name = arg.data();
        do arg.dispose();
        do Eval.install(name, FUNC, LibSym.copyList(argv));
        do LibSym.clearList(argv);

        return;
    }


    function Symbol doLength(Collection argv) {
        var int n;
        var String s;
        var Collection l;
        var Symbol p;

        let p = Eval.arg(argv.pop());

        if (p = null) {
            do LibIO.error("expected iterable");
            return null;
        }

        if (p.meta() = STR) {
            let s = p.data();
            let n = s.length();
        } else { if (p.meta() = LIST) {
            let l = p.data();
            let n = l.length();
        } else {
            do LibIO.error("expected iterable");
        }}

        do LibSym.destroySym(p);

        return Symbol.new(null, INT, n);
    }


    function void doIf(Collection argv) {
        var Collection elsev;
        var Symbol arg;

        let arg = Eval.arg(argv.pop());
        let elsev = Exec.elsev(argv);

        if (arg.data() = 0) {
            if (~(elsev = null)) {
                do Main.try(elsev);
            }
        } else {
            do Main.try(argv);
        }

        do LibSym.destroySym(arg);
        do LibSym.destroyList(argv);
        do LibSym.destroyList(elsev);

        return;
    }


    function Collection elsev(Collection argv) {
        var int a;
        var int nif;
        var Symbol arg;

        let nif = 0;

        let a = 0;
        while (a < argv.length()) {
            let arg = argv.nth(a);
            if (arg.meta() = NAME) {
                if (Str.eq(ELSES, arg.data())) {
                    if (nif = 0) {
                        return Exec.elseVector(argv, a);
                    } else { if (nif < 0) {
                        do LibIO.error("unexpected 'ELSE'");
                        return null;
                    } else {
                        let nif = nif - 1;
                    }}
                } else { if (Str.eq(IFS, arg.data())) {
                    let nif = nif + 1;
                }}
            }
            let a = a + 1;
        }

        return null;
    }


    /* Collection.slice() would be easier to read but redundant to execute. */
    function Collection elseVector(Collection argv, int a) {
        var Collection elsev;

        let elsev = Collection.new();
        while ( argv.length() > (a+1) ) {
            do elsev.append(argv.remove(a+1));
        }

        do LibSym.destroySym(argv.remove(a));

        return elsev;
    }


    function void doRepeat(Collection argv) {
        var int i;
        var Symbol arg;

        let arg = Eval.arg(argv.pop());

        if (arg = null) {
            do LibIO.error("expected integer");
            return;
        }

        if ( (arg.meta()=CMD) & (arg.data()=IF) ) {
            do arg.dispose();
            do Exec.doRepeatIf(argv);
            return;
        }

        if ( (arg.meta()=CMD) & (arg.data()=OVER) ) {
            do arg.dispose();
            do Exec.doRepeatOver(argv);
            return;
        }

        if (~(arg.meta() = INT)) {
            do LibIO.error("won't repeat that");
            do LibSym.destroySym(arg);
            return;
        }

        let i = arg.data();
        do arg.dispose();
        while (i > 0) {
            do Main.try(LibSym.copyList(argv));
            let i = i - 1;
        }

        do LibSym.clearList(argv);

        return;
    }


    function void doRepeatIf(Collection argv) {
        var int condition;
        var Symbol arg;

        let arg = argv.pop();

        let condition = Eval.condition(arg);
        while (~(condition = 0)) {
            do Main.try(LibSym.copyList(argv));
            let condition = Eval.condition(arg);
        }

        do LibSym.clearList(argv);
        do LibSym.destroySym(arg);

        return;
    }


    function void doRepeatOver(Collection argv) {
        var int i;
        var String name;
        var Collection list;
        var Symbol arg;

        let arg = Eval.arg(argv.pop());

        if (arg = null) {
            do LibIO.error("expected list");
            return;
        }

        if (~(arg.meta() = LIST)) {
            do LibIO.error("expected list");
            do LibSym.destroySym(arg);
            return;
        }

        let list = arg.data();
        do arg.dispose();

        let arg = argv.pop();

        if (arg = null) { // todo: error
            return;
        }

        if (~(arg.meta() = NAME)) { // todo: error
            return;
        }

        let name = arg.data();

        let i = 0;
        while (i < list.length()) {
            let arg = list.nth(i);
            do Eval.install(name, arg.meta(), arg.data());
            do Main.try(LibSym.copyList(argv));
            let i = i + 1;
        }

        return;
    }


    function void doQuit(Symbol arg) {
        do Main.stop();  /* syscall! */
        return;
    }

}
