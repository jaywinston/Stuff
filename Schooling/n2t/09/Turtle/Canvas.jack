class Canvas {

    static int bottom, left, right, top;
    static int braces;
    static int repeat;
    static String PS1, PS2, ps;
    static String fName;
    static TextBox tb;
    static Turtle t;
    static Symbol block, blocks, cmdp, cmds;

    static int
        NULL,
        BLK,
        CMD,
        CMDLIN,
        FUNC,
        INT,
        STR,
        SYM,

        ASSGN,
        BLOCK,
        CLEAR,
        FORWARD,
        FUNCTION,
        IF,
        PENDOWN,
        PENUP,
        PRINT,
        REPEAT,
        TURNLEFT,
        TURNRIGHT,
        QUIT,
        _ECHO;


    function void init(int i, int j, int h, int w, String ps1, String ps2) {

        do Canvas.initKWs();

        let top = i;
        let left = j;
        let bottom = i + h;
        let right = j + w;
        let block = null;
        let blocks = null;
        let braces = 0;
        let repeat = 1;
        let fName = null;
        let PS1 = ps1;
        let PS2 = ps2;
        let ps = PS1;

        let tb = TextBox.new(i, j, h, w);
        let t = Canvas.newTurtle();

        return;
    }


    function void dispose() {
        do PS1.dispose();
        do PS2.dispose();
        do TKN_DLMS.dispose();
        do EXP_DLMS.dispose();
        do t.dispose();
        do tb.dispose();
        return;
    }


    /* intialize key words and internal symbols */
    function void initKWs() {
        var int i;

        let NULL   = Const.NULL();
        let BLK    = Const.BLK();
        let CMD    = Const.CMD();
        let CMDLIN = Const.CMDLIN();
        let FUNC   = Const.FUNC();
        let INT    = Const.INT();
        let STR    = Const.STR();
        let SYM    = Const.SYM();

        let ASSGN     = Const.ASSGN();
        let BLOCK     = Const.BLOCK();
        let CLEAR     = Const.CLEAR();
        let FORWARD   = Const.FORWARD();
        let FUNCTION  = Const.FUNCTION();
        let IF        = Const.IF();
        let PENDOWN   = Const.PENDOWN();
        let PENUP     = Const.PENUP();
        let PRINT     = Const.PRINT();
        let REPEAT    = Const.REPEAT();
        let TURNLEFT  = Const.TURNLEFT();
        let TURNRIGHT = Const.TURNRIGHT();
        let QUIT      = Const.QUIT();

        return;
    }


    function void clear() {
        do t.dispose();
        do tb.clear();
        let t = Canvas.newTurtle();
        return;
    }


    function Turtle newTurtle() {
        return Turtle.new(((2*left) + (right-left)) * 4,
                           ((2*bottom) + (top-bottom)) * 5,
                           90);
    }


    /* prepare to execute next command line */
    function Symbol ready(Symbol p) {

        do Canvas.clearSym(p);

        if (Eval.isStoring()) {
            return PS2;
        }

        return PS1;
    }


    /* execute a command line */
    function Symbol exec(String s) {
        var int cmd;
        var Symbol argv, retval;

        if (s = null) {
            return null;
        }

        if (s.length() = 0) {
            return null;
        }

        let argv = Eval.eval(s);

        if (Error.isSet()) {
            do Canvas.clearSym(argv);
            return null;
        }

        if (argv = null) {
            return null;
        }

        if (argv.type() = NULL) {
            do Error.send("unrecognized token"); // ##DB##
            do Error.set(1);  // unrecognized token
            do Canvas.clearSym(argv);
            return null;
        }

        if (argv.type() = FUNC) {
            let retval = Canvas.call(argv);
        } else {
            let retval = Canvas.execute(argv);
        }

        do Canvas.clearSym(argv);

        return retval;
    }

    function Symbol execute(Symbol argv) {
        var int cmd;

        if (argv = null) {
            return null;
        }

        if (~(argv.type() = CMD)) {
            return Canvas.dupSym(argv);
        }

        let cmd = argv.value();

        if (cmd = ASSGN)            { return Canvas.doAssign(argv.next());    }
        else { if (cmd = BLOCK)     { return Canvas.doBlock(argv);            }
        else { if (cmd = CLEAR)     { return Canvas.doClear(argv.next());     }
        else { if (cmd = FORWARD)   { return Canvas.doForward(argv.next());   }
        else { if (cmd = FUNCTION)  { return Canvas.doFunction(argv.next());  }
        else { if (cmd = IF)        { return Canvas.doIf(argv.next());        }
        else { if (cmd = PENDOWN)   { return Canvas.doPendown(argv.next());   }
        else { if (cmd = PENUP)     { return Canvas.doPenup(argv.next());     }
        else { if (cmd = PRINT)     { return Canvas.doPrint(argv.next());     }
        else { if (cmd = REPEAT)    { return Canvas.doRepeat(argv.next());    }
        else { if (cmd = TURNLEFT)  { return Canvas.doTurnleft(argv.next());  }
        else { if (cmd = TURNRIGHT) { return Canvas.doTurnright(argv.next()); }
        else { if (cmd = QUIT)      { return Canvas.doQuit(argv.next());      }
        }}}}}}}}}}}}

        return null;
    }

    function Symbol call(Symbol argv) {
        var int n, type;
        var Array farg;
        var String arg, name, fpres;
        var Symbol p;

        let farg = Array.new(1);
        let fpres = "$%d";
        let n = 1;
        let p = argv.next();
        while (~(p = null)) {
            let farg[0] = n;
            let name = Str.format(fpres, farg);
            do symtab.install(name, p.type(), p.value());
            do name.dispose();
            let n = n + 1;
            let p = p.next();
        }

        let p = Canvas.execute(argv.value());

        do farg.dispose();
        do fpres.dispose();

        return p;
    }


    function Symbol execBlock(Symbol b) {
        var Symbol p;

        while (~(b = null)) {
            if (~(b.type() = CMDLIN)) {
                return p;
            }
            do Canvas.clearSym(p);
            let p = Canvas.exec(b.value());
            let b = b.next();
        }

        return p;
    }


    function void doAssign(Symbol arg) {
        do Eval.assign(arg.value());
        return;
    }


    function Symbol doBlock(Symbol arg) {
        return Canvas.execBlock(arg.next());
    }


    function void doClear(Symbol arg) {
        do Canvas.clear();
        return;
    }


    function void doForward(Symbol arg) {

        if (arg.type() = CMD) {
            let arg = t.forward(Canvas.execBlock(arg));
        }

        do t.forward(arg.value());

        return;
    }


    function void doFunction(Symbol arg) {

        if (arg = null) {
            do Error.set(1);  // no function name
            return;
        }

        if (~(arg.type() = SYM)) {
            //do Error.set(1);  // no function name
            //return;
        }

        do Eval.install(arg.name(), FUNC, Canvas.dupSym(arg.next()));

        return;
    }


    function Symbol doIf(Symbol arg) {

        if (~(arg.value() = 0)) {
            return Canvas.execute(arg.next());
        }

        return null;
    }

    function void doPendown(Symbol arg) {
        do t.pendown();
        return;
    }


    function void doPenup(Symbol arg) {
        do t.penup();
        return;
    }


    function void doPrint(Symbol arg) {
        return;
    }


    function void doRepeat(Symbol arg) {
        let repeat = arg.value();
        return;
    }


    function void doTurnleft(Symbol arg) {
        do t.turnleft(arg.value());
        return;
    }


    function void doTurnright(Symbol arg) {
        do t.turnright(arg.value());
        return;
    }


    function void doQuit(Symbol arg) {
        do Sys.halt();
        return;
    }

/*
    function void echo(Symbol sym) {
        if (sym.type() = STR) {
            do Error.send(sym.value());
        } else {
            do echoInt(sym);
        }
        return;
    }

    function void echoInt(Symbol sym) {
        var String s;

        let s = String.new(5);
        do s.setInt(sym.value());
        do Error.send(s);
        return;
    }
*/


    function void pushBlock(Symbol blk) {
        var Symbol top;

        let top = Symbol.new(null, SYM, blk);
        do top.append(blocks);
        let blocks = top;
        return;
    }


    function Symbol popBlock() {
        var Symbol blk, top;

        let top = blocks;
        let blocks = blocks.next();
        let blk = top.value();
        do top.dispose();
        return blk;
    }


    function void pushCmd(Symbol cmd) {
        var Symbol top;

        let top = Symbol.new(null, SYM, cmd);
        do top.append(cmds);
        let cmds = top;
        return;
    }


    function Symbol popCmd() {
        var Symbol cmd, top;

        let top = cmds;
        let cmds = cmds.next();
        let cmd = top.value();
        do top.dispose();
        return cmd;
    }


    function void clearSym(Symbol sym) {
        var int type;
        var String s;
        var Symbol p;

        while (~(sym = null)) {
            let p = sym;
            let sym = sym.next();

            let type = p.type();
            if ( (type=STR) | (type=CMDLIN) ) {
                let s = p.value();
                if (~(s = null)) {
                    do s.dispose();
                }
            } else { if (type = BLK) {
                do Canvas.clearSym(p.value());
            }}
            do p.dispose();
        }

        return;
    }

    function Symbol dupSym(Symbol sym) {
        var int type;
        var Symbol p;

        let p = Symbol.new(null, null, null);

        while (~(sym = null)) {

            let type = sym.type();

            if ( (type=STR) | (type=CMDLIN) ) {
                do p.append(Symbol.new(sym.name(),
                                       sym.type(),
                                       Str.dup(sym.value())));
            } else { if (type = BLK) {
                do p.append(Symbol.new(sym.name(),
                                       sym.type(),
                                       Canvas.dupSym(sym.value())));
            } else {
                do p.append(Symbol.new(sym.name(), sym.type(), sym.value()));
            }}

            let sym = sym.next();
        }

        let sym = p;
        let p = p.next();
        do sym.setNext(null);
        do Canvas.clearSym(sym);

        return p;
    }

}
