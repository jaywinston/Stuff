class Canvas {

    static int bottom, left, right, top;
    static int braces;
    static int repeat;
    static String fName;
    static TextBox tb;
    static Turtle t;

    static int
        NULL,
        BLK,
        CMD,
        CMDLIN,
        FUNC,
        INT,
        STR,
        SYM,

        ASSGN,
        BLOCK,
        CLEAR,
        FORWARD,
        FUNCTION,
        IF,
        PENDOWN,
        PENUP,
        PRINT,
        REPEAT,
        TURNLEFT,
        TURNRIGHT,
        QUIT,
        _ECHO;


    function void init(int i, int j, int h, int w) {

        do Canvas.initKWs();
        let tb = TextBox.new(i, j, h, w);

        let top = i;
        let left = j;
        let bottom = i + h;
        let right = j + w;

        do Canvas.getTurtle();

        return;
    }


    /* intialize key words and internal symbols */
    function void initKWs() {
        var int i;

        let NULL   = Const.NULL();
        let BLK    = Const.BLK();
        let CMD    = Const.CMD();
        let CMDLIN = Const.CMDLIN();
        let FUNC   = Const.FUNC();
        let INT    = Const.INT();
        let STR    = Const.STR();
        let SYM    = Const.SYM();

        let ASSGN     = Const.ASSGN();
        let BLOCK     = Const.BLOCK();
        let CLEAR     = Const.CLEAR();
        let FORWARD   = Const.FORWARD();
        let FUNCTION  = Const.FUNCTION();
        let IF        = Const.IF();
        let PENDOWN   = Const.PENDOWN();
        let PENUP     = Const.PENUP();
        let PRINT     = Const.PRINT();
        let REPEAT    = Const.REPEAT();
        let TURNLEFT  = Const.TURNLEFT();
        let TURNRIGHT = Const.TURNRIGHT();
        let QUIT      = Const.QUIT();

        return;
    }


    function void clear() {
        do t.dispose();
        do tb.clear();
        do Canvas.getTurtle();
        return;
    }


    function void getTurtle() {
        let t = Turtle.new(((2*left) + (right-left)) * 4,
                          ((2*bottom) + (top-bottom)) * 5,
                          90, top, left, bottom, right);
        return;
    }


    /* execute a command line */
    //function Symbol exec(String s) {
    function Symbol exec(Symbol s) {
        var int cmd;
        var Symbol argv, retval;

        if (s = null) {
            return null;
        }

/*
        if (s.length() = 0) {
            return null;
        }
*/

        //let argv = Eval.eval(s);
        let argv = s;

        if (Error.isSet()) {
            do Lib.clearSym(argv);
            return null;
        }

        if (argv = null) {
            return null;
        }

        if (argv.type() = NULL) {
            do Error.send("unrecognized token"); // ##DB##
            do Error.set(1);  // unrecognized token
            do Lib.clearSym(argv);
            return null;
        }

        if (argv.type() = FUNC) {
            return Canvas.call(argv);
            //let retval = Canvas.call(argv);
        } //else {
            return Canvas.execute(argv);
            //let retval = Canvas.execute(argv);
        //}
    }

    function Symbol execute(Symbol argv) {
        var int cmd;

        if (argv = null) {
            return null;
        }

        if (argv.type() = BLOCK)  { return Canvas.execBlock(argv.value()); }
        if (argv.type() = CMDLIN) { return Canvas.execute(argv.value()); }
        if (~(argv.type() = CMD)) { return Lib.dupSym(argv);             }

        let cmd = argv.value();

        if (cmd = ASSGN)            { do Canvas.doAssign(argv.next());    }
        else { if (cmd = BLOCK)     { do Canvas.doBlock(argv);            }
        else { if (cmd = CLEAR)     { do Canvas.doClear(argv.next());     }
        else { if (cmd = FORWARD)   { do Canvas.doForward(argv.next());   }
        else { if (cmd = FUNCTION)  { do Canvas.doFunction(argv.next());  }
        else { if (cmd = IF)        { do Canvas.doIf(argv.next());        }
        else { if (cmd = PENDOWN)   { do Canvas.doPendown(argv.next());   }
        else { if (cmd = PENUP)     { do Canvas.doPenup(argv.next());     }
        else { if (cmd = PRINT)     { do Canvas.doPrint(argv.next());     }
        else { if (cmd = REPEAT)    { do Canvas.doRepeat(argv.next());    }
        else { if (cmd = TURNLEFT)  { do Canvas.doTurnleft(argv.next());  }
        else { if (cmd = TURNRIGHT) { do Canvas.doTurnright(argv.next()); }
        else { if (cmd = QUIT)      { do Canvas.doQuit(argv.next());      }
        }}}}}}}}}}}}

        do Lib.clearSym(argv);

        return null;
    }

    function Symbol call(Symbol argv) {
        var int n, type;
        var Array farg;
        var String arg, name, fpres;
        var Symbol p;

        let farg = Array.new(1);
        let fpres = "$%d";
        let n = 1;
        let p = argv.next();
        while (~(p = null)) {
            let farg[0] = n;
            let name = Str.format(fpres, farg);
            do Eval.install(name, p.type(), p.value());
            do name.dispose();
            let n = n + 1;
            let p = p.next();
        }

        let p = Canvas.execute(argv.value());

        do farg.dispose();
        do fpres.dispose();

        return p;
    }


/*
    function Symbol execBlock(Symbol b) {
        var Symbol p;

        while (~(b = null)) {
            do Lib.clearSym(p);
            let p = Canvas.execute(b.value());
            let b = b.next();
        }

        return p;
    }
*/

    function Symbol execBlock(Symbol b) {

        while (~(b.next() = null)) {
            do Lib.clearSym(Canvas.execute(b.value()));
            let b = b.next();
        }

        return Canvas.execute(b.value());
    }


    function void doAssign(Symbol arg) {
        do Eval.assign(arg.value());
        return;
    }


    function Symbol doBlock(Symbol arg) {
        return Canvas.execBlock(arg.next());
    }


    function void doClear(Symbol arg) {
        do Canvas.clear();
        return;
    }


    function void doForward(Symbol arg) {

        if (arg.type() = BLOCK) {
            let arg = Canvas.execBlock(arg);
        }

        do t.forward(arg.value());

        return;
    }


    function void doFunction(Symbol arg) {

        if (arg = null) {
            do Error.set(1);  // no function name
            return;
        }

        do Eval.install(arg.name(), FUNC, arg.next());

        return;
    }


    function void doIf(Symbol arg) {

        if (~(arg.value() = 0)) {
            do Lib.clearSym(Canvas.execute(arg.next()));
        }

        return;
    }

    function void doPendown(Symbol arg) {
        do t.pendown();
        return;
    }


    function void doPenup(Symbol arg) {
        do t.penup();
        return;
    }


    function void doPrint(Symbol arg) {
        return;
    }


    function void doRepeat(Symbol arg) {
        let repeat = arg.value();
        return;
    }


    function void doTurnleft(Symbol arg) {
        do t.turnleft(arg.value());
        return;
    }


    function void doTurnright(Symbol arg) {
        do t.turnright(arg.value());
        return;
    }


    function void doQuit(Symbol arg) {
        do Sys.halt();
        return;
    }


    function void try(Symbol retVal) {

        if (Error.isSet()) {
            do Terminal.echo(Error.message());
            if (Error.isSet()) {
                do Terminal.echo(Error.message());
            }
        } else {
            do Canvas.echo(retVal);
        }

        return;
    }


    function void echo(Symbol sym) {
        while (~(sym = null)) {
            if (sym.type() = STR) {
                do Terminal.echo(sym.value());
            } else { if (sym.type() = SYM) {
                do Console.echo(sym.value());
            } else { if (sym.type() = INT) {
                do Console.echoInt(sym);
            }}}
            let sym = sym.next();
        }
        return;
    }


    function void echoInt(Symbol sym) {
        var String s;

        let s = String.new(6);
        do s.setInt(sym.value());
        do Terminal.echo(s);
        do s.dispose();
        return;
    }

}
