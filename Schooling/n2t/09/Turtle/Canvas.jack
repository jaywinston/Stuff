class Canvas {

    field int top, left, height, width;
    field String /*INBUF,*/ PS1, PS2, ps;
    field Turtle t;
    field TextBox tb;

    static int PENUP, PENDOWN, TURNLEFT, TURNRIGHT, FORWARD, BLOCK;
    static Array ERRTAB;

    constructor Canvas new(int i, int j, int h, int w) {
        let top = i;
        let left = j;
        let height = h;
        let width = w;
        let PS1 = ">";
        let PS2 = "|";
        let ps = PS1;
        let tb = TextBox.new(top, left, height, width);
        do getTurtle();
        return this;
    }

    method void dispose() {
        do t.dispose();
        do tb.dispose();
        do Memory.deAlloc(this);
        return;
    }

    function void init() {
        var String s;

        let s = "penup";
        let PENUP     = lib.hash(s);
        do s.dispose();
        let s = "pendown";
        let PENDOWN   = lib.hash(s);
        do s.dispose();
        let s = "turnleft";
        let TURNLEFT  = lib.hash(s);
        do s.dispose();
        let s = "turnright";
        let TURNRIGHT = lib.hash(s);
        do s.dispose();
        let s = "forward";
        let FORWARD   = lib.hash(s);
        do s.dispose();
        let s = "block";
        let BLOCK     = lib.hash(s);
        do s.dispose();

        let ERRTAB[1] = "too many arguments";
        let ERRTAB[2] = "too few arguments";
        let ERRTAB[3] = "wrong type; expected integer";

        return;
    }

    method void clear() {
        do tb.clear();
        do t.dispose();
        do getTurtle();
        return;
    }

    method void getTurtle() {
        let t = Turtle.new(((left*2)+width)*4,
                           (top+(height/2))*11, 90);
        return;
    }

    method String exec(String s, List errno) {
        var int tknhash;
        var int n;
        var List argv, p;

        if (s.length() = 0) {
            return ps;
        }

        let argv = Parser.parse(s);

        if (argv = null) {
            return ps;
        }

        let tknhash = argv.object();
        let p = argv;
        let argv = argv.next();
        do p.setNext(null);
        do p.dispose();

        if (tknhash = PENUP) {
            if (~(argv = null)) {
                do errno.setObject(1);
            } else {
                do t.penup();
            }
        }
        if (tknhash = PENDOWN) {
            if (~(argv = null)) {
                do errno.setObject(1);
            } else {
                do t.pendown();
            }
        }
        if (tknhash = TURNLEFT) {
            if (argv = null) {
                do errno.setObject(2);
            } else { if (~(argv.next() = null)) {
                do errno.setObject(1);
            } else {
                do t.turnleft(argv.object());
            }}
        }
        if (tknhash = TURNRIGHT) {
            if (argv = null) {
                do errno.setObject(2);
            } else { if (~(argv.next() = null)) {
                do errno.setObject(1);
            } else {
                do t.turnright(argv.object());
            }}
        }
        if (tknhash = FORWARD) {
            if (argv = null) {
                do errno.setObject(2);
            } else { if (~(argv.next() = null)) {
                do errno.setObject(1);
            } else {
                do t.forward(argv.object());
            }}
        }
        if (tknhash = BLOCK) {
            let ps = PS2;
        }

        do argv.dispose();
        return ps;
    }

    method String error(int errno) {
        return ERRTAB[errno];
    }
}
