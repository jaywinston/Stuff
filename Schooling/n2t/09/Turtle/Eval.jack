class Eval {

    static boolean storing;
    static int state, ININTBLOCK, REPEXPECTEXPR, IFEXPECTEPXR, EXPECTCMDLIN,
                STORING;
    static int ASSGN, BLOCK, CMD, CMDLIN, INT, STR, SYM;
    static int TERM;
    static Array INTOPS;
    static String PS2;
    static String WORDDLMS, EXPDLMS;
    static String BLANKS, EQS, LBRACES, PERCENTS;
    static Symbol block, stack;
    static HashTable symtab;

    function void init() {

        let storing = false;
        let state         = 0;
        let REPEXPECTEXPR = 1;
        let IFEXPECTEPXR  = 2;
        let EXPECTCMDLIN  = 3;
        let STORING       = 4;
        let ININTBLOCK    = 5;

        let ASSGN = Const.ASSGN();
        let BLOCK = Const.BLOCK();
        let CMD = Const.CMD();
        let CMDLIN = Const.CMDLIN();
        let INT = Const.INT();
        let STR = Const.STR();
        let SYM = Const.SYM();

        let TERM = 4;

        let WORDDLMS = String.new(4);
        do WORDDLMS.appendChar(32);   // ' '
        do WORDDLMS.appendChar(34);   // '"'
        do WORDDLMS.appendChar(61);   // '='
        do WORDDLMS.appendChar(123);  // '{'

        let PS2 = "|";

        let EXPDLMS = "=!<>&|+-*/%";
        let BLANKS = " ";
        let EQS = "=";
        let LBRACES = "{";
        let PERCENTS = "%";

        let INTOPS = Array.new(4);
        let INTOPS[0] = "&|(){}";
        let INTOPS[1] = "=!<>(){}";
        let INTOPS[2] = "+-(){}";
        let INTOPS[3] = "*/%(){}";

        let symtab = HashTable.new();

        do Eval.installKW("CLEAR",     Const.CLEAR());
        do Eval.installKW("FORWARD",   Const.FORWARD());
        do Eval.installKW("FUNCTION",  Const.FUNCTION());
        do Eval.installKW("IF",        Const.IF());
        do Eval.installKW("PENDOWN",   Const.PENDOWN());
        do Eval.installKW("PENUP",     Const.PENUP());
        do Eval.installKW("PRINT",     Const.PRINT());
        do Eval.installKW("REPEAT",    Const.REPEAT());
        do Eval.installKW("TURNLEFT",  Const.TURNLEFT());
        do Eval.installKW("TURNRIGHT", Const.TURNRIGHT());
        do Eval.installKW("QUIT",      Const.QUIT());

        let stack = null;

        return;
    }

    function void installKW(String name, int val) {
        do symtab.install(name, CMD, val);
        do name.dispose();
        return;
    }

    function Symbol eval(String s) {
        var char c;
        var int i, j;
        var String t;

        if (s.length() = 0) {
            return null;
        }

        if (storing) {
            do Eval.store(s);
            return null;
        }

        return Eval.argList(s, 0);
    }

    function void store(String s) {
        return;
    }

    function Symbol argList(String s, int i) {
        var String t;
        var Symbol argv;

        if (i > s.length()) {
            return null;
        }

        let i = Str.lskip(s, 32, i);

        if (i = s.length()) {
            return null;
        }

        let t = String.new(s.length());
        let i = Eval.argTkn(s, t, i);

        if (Error.isSet()) {
            do t.dispose();
            return null;
        }

        if (Eval.isAssign(t)) {
            let argv = Symbol.new(null, CMD, ASSGN);
            do argv.append(Symbol.new(null, CMDLIN, t));
        } else {
            let argv = Eval.expression(t);
            do t.dispose();
        }

        if (Error.isSet()) {
            do Canvas.clearSym(argv);
            return null;
        }

        do argv.append(Eval.argList(s, i+1));

        return argv;
    }

    function int argTkn(String s, String t, int i) {

        let i = Str.tkn(s, t, WORDDLMS, i);

        if (i < s.length()) {
            if (s.charAt(i) = 34) {
                let i = Str.quotes(s, t, i);
                if (i > s.length()) {
                    do Error.send("unclosed quote");  // ##DB##
                    do Error.set(1);  // unclosed quote
                }
            } else { if (s.charAt(i) = 123) {
                let i = Str.block(s, t, 123, 125, i);
            } else { if (s.charAt(i) = 61) {
                do t.appendChar(61);
                let i = Eval.argTkn(s, t, i+1);
            }}}
        }

        if (i < s.length()) { if (~(s.charAt(i) = 32)) {
            let i = Eval.argTkn(s, t, i);
        }}

        return i;
    }

    function boolean isAssign(String s) {
        var int i;

        let i = Str.index(s, WORDDLMS, 0);

        if (i < s.length()) {
            if (s.charAt(i) = 61) {
                if ( (i+1) < s.length() ) { if (s.charAt(i+1) = 61) {
                    return false;
                }} else { if (i > 0) {
                        if (  (s.charAt(i-1) = 33)
                           | ((s.charAt(i-1) = 60)
                           |  (s.charAt(i-1) = 62))
                           ) {
                    return false;
                }}}
                return true;
            }
        }

        return false;
    }

    function void assign(String s) {
        var String name;
        var Symbol value;

        if (s.charAt(0) = 61) {
            do Error.send("expected name");  // ##DB##
            do Error.set(1);  // expected name
            return;
        }

        let name = String.new(s.length());
        let value = Eval.argList(s, Str.tkn(s, name, EQS, 0)+1);

        if (Error.isSet()) {
            do name.dispose();
            do Canvas.clearSym(value);
            return;
        }

        if (value.type() = STR) {
            do symtab.install(name, value.type(), Str.dup(value.value()));
        } else {
            do symtab.install(name, value.type(), value.value());
        }

        do name.dispose();
        do Canvas.clearSym(value);

        return;
    }


    function Symbol expression(String s) {
        var char c;

        let c = s.charAt(0);

        if (c = 34) {
            return Symbol.new(null, STR, Eval.StringExpr(s));
        }

        if ( (Str.index(s, EXPDLMS, 0) = s.length()) & ~((c>47) & (c<58)) ) {
            return Eval.symbolExpr(s);
        }

        /* todo: fix this, it precludes intexpr start with block */
        if ( (c=123)  ) {
            return Eval.blockExpr(s);
        }

        return Symbol.new(s, INT, Eval.intExpr(s, 0, 0));
    }


    /* todo: use Str.format(), no destruct, symbol param */
    function String StringExpr(String s) {
        do s.eraseLastChar();
        do Str.shift(s, 1, 0);
        return Str.dup(Eval.format(s));
    }


    function String format(String s) {
        var int i;

        let i = 0;

        while (i < s.length()) {

            let i = Str.index(s, PERCENTS, i);

            if ((i+1) < s.length()) {
                if (s.charAt(i+1) = 81) {
                    do s.setCharAt(i, 34);
                } else { if (s.charAt(i+1) = 80) {
                    do s.setCharAt(i, 37);
                } else { if (s.charAt(i+1) = 78) {
                    do s.setCharAt(i, 128);
                } else {
                    do Error.send("unrecognized format specifier");  // ##DB##
                    do Error.set(1);
                    do t.dispose();
                    return null;
                }}}

                let i = i + 1;
                do Str.shift(s, 1, i);

            }  // todo: error no fspec
        }

        return s;
    }


    function Symbol blockExpr(String s) {
        var int braces;
        var Symbol block;

        let block = Symbol.new(null, CMD, BLOCK);
        let braces = Str.count(s, 123) - Str.count(s, 125);

        let s = Str.dup(s);
        do Str.shift(s, 1, 0);

        while (braces > 0) {
            do block.append(Symbol.new(null, CMDLIN, s));
            let s = Str.dup(Console.read(PS2));
            let braces = braces + (Str.count(s, 123) - Str.count(s, 125));
        }

        if (braces < 0) {
            do Error.send("unexpected right brace");  // ##DB##
            do Error.set(1); // unexpected right brace
            do Canvas.clearSym(block);
            return null;
        }

        do s.eraseLastChar();
        do block.append(Symbol.new(null, CMDLIN, s));

        return block;
    }

    function Symbol symbolExpr(String s) {
        var Symbol p;

        let p = symtab.lookup(s);

        if (p = null) {
            return Symbol.new(s, SYM, null);
        }

        return Symbol.new(s, p.type(), p.value());
    }

    function int intExpr(String s, int i, int depth) {
        var char op;
        var int n;
        var String t;

        if (i = s.length()) {
            do Error.send("expected integer expression");  // ##DB##
            do Error.set(1);  //  expected integer expression
            return 0;
        }
            
        if (depth = TERM) {
            return Eval.term(s);
        }

        if (Error.isSet()) {
            return 0;
        }

        if (s.charAt(i) = 45) {
            return -Eval.intExpr(s, i+1, depth);
        }

        if (s.charAt(i) = 126) {
            return ~Eval.intExpr(s, i+1, depth);
        }

        let t = String.new(s.length());
        let i = Eval.intTkn(s, t, INTOPS[depth], i);

        if (Error.isSet()) {
            do t.dispose();
            return 0;
        }

        if (t.length() = 0) {
            do Error.send("unexpected operator");  // ##DB##
            do Error.set(1);  // unexpected operator
            do t.dispose();
            return 0;
        }

        let n = Eval.intExpr(t, 0, depth+1);

        do t.dispose();

        if (i < s.length()) {

            let op = s.charAt(i);

            if ( (i+1) < s.length() ) { if (s.charAt(i+1) = 61) {
                let op = op + 200;
                let i = i + 1;
            }}

            let n = Eval.operate(n, Eval.intExpr(s, i+1, depth), op);
        }

        return n;
    }

    function int term(String s) {
        var char c;
        var Symbol p;

        let c = s.charAt(0);

        if ( (c>47) & (c<58) ) {
            return s.intValue();
        }

        if (c = 40) {
            return Eval.parenTerm(s);
        }

        if (c = 123) {
            return Eval.blockTerm(s);
        }

        if (Error.isSet()) {
            return 0;
        }

        return Eval.symbolTerm(s);

    }

    function int parenTerm(String s) {

        do s.eraseLastChar();
        do Str.shift(s, 1, 0);

        return Eval.intExpr(s, 0, 0);
    }

    function int blockTerm(String s) {
        var int n;
        var Symbol b, p;

        let b = Eval.blockExpr(s);
        let p = Canvas.execBlock(b.next());
        do Canvas.clearSym(b);

        if (~(p.type() = INT)) {
            do Error.send("wrong type; expected integer");  // ##DB##
            do Error.set(1);  // wrong type; expected integer
        }

        let n = p.value();

        do Canvas.clearSym(p);

        return n;
    }

    function int symbolTerm(String s) {
        var Symbol p;

        let p = symtab.lookup(s);

        if (~(p.type() = INT)) {
            do Error.send("wrong type; expected integer");  // ##DB##
            do Error.set(1);  // wrong type; expected integer
            return 0;
        }

        return p.value();
    }

    function int intTkn(String s, String t, String list, int i) {

        let i = Str.tkn(s, t, list, i);

        if (i < s.length()) {
            if (s.charAt(i) = 40) {
                let i = Str.block(s, t, 40, 41, i);
                if (i > s.length()) {
                    do Error.send("unclosed parentheses");  // ##DB##
                    do Error.set(1);  // unclosed parentheses
                    return i;
                }
                if (i < s.length()) {
                    let i = Eval.intTkn(s, t, list, i);
                }
            } else { if (s.charAt(i) = 123) {
                //do Eval.push(t);
                //let t = String.new(s.length());
                let i = Str.block(s, t, 123, 125, i);
/*
                if (i < s.length()) {
                    do Str.cat(stack.value(), t);
                    do t.dispose();
                    let t = Eval.pop();
                    let i = Eval.intTkn(s, t, list, i);
                } else { if (i > s.length()) {
                    let storing = true;
                }}
*/
            }}
        }

        return i;
    }

    function int operate(int m, int n, char op) {

        if ( ((op=47) | (op=37)) & (n=0) ) {
            do Error.send("division by zero");  // ##DB##
            do Error.set(1);  // division by zero
            return 0;
        }

        if (op =  38) { return m & n; }
        if (op = 124) { return m | n; }

        if (op =  43) { return m + n; }
        if (op =  45) { return m - n; }
        if (op =  42) { return m * n; }
        if (op =  47) { return m / n; }
        if (op =  37) { return lib.mod(m, n); }

        if (op = 261) { return   m = n;  }
        if (op = 233) { return ~(m = n); }
        if (op =  60) { return   m < n;  }
        if (op =  62) { return   m > n;  }
        if (op = 260) { return  (m<n) | (m=n); }
        if (op = 262) { return  (m>n) | (m=n); }

        do Error.send("unrecognized operator");  // ##DB##
        do Error.set(1);  // unrecognized operator
        return 0;
    }

    function void install(String name, int type, int value) {
        do symtab.install(name, type, value);
        return;
    }


    function boolean isStoring() {
        return storing;
    }

}
