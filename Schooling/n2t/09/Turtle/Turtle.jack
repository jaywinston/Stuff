class Turtle {

    field int direction, _x, _y;
    field Vector v;
    field boolean _pendown;

    constructor Turtle new(int x, int y, int dir) {
        let _x = x;
        let _y = y;
        let direction = setDirection(dir);
        let v = Vector.new();
        let _pendown = false;
        do show();
        return this;
    }

    method int setDirection(int val) {
        let direction = util.mod(val, 360);
        if (direction < 0) {
            let direction = direction + 360;
        }
        return direction;
    }

    method int setX(int val) {
        let _x = val;
        return _x;
    }

    method int setY(int val) {
        let _y = val;
        return _y;
    }

    method int direction() {
        return direction;
    }

    method int x() {
        return _x;
    }

    method int y() {
        return _y;
    }

    method void drawTurtle() {
        var int i, l, tlen;

        let tlen = 10;
        let i = 150;
        let l = 0;
        while (i < 180) {
            do v.calculate(tlen-(l/6), direction+i);
            do Screen.drawLine(_x, _y, _x+v.x(), _y-v.y());
            let i = i + 1;
            let l = l + 1;
        }
        do v.calculate(tlen-(l/6), direction+i);
        do Screen.drawLine(_x, _y, _x+v.x(), _y-v.y());
        while (i < 210) {
            do v.calculate(tlen-(l/6), direction+i);
            do Screen.drawLine(_x, _y, _x+v.x(), _y-v.y());
            let i = i + 1;
            let l = l - 1;
        }
        do v.calculate(tlen-(l/6), direction+i);
        do Screen.drawLine(_x, _y, _x+v.x(), _y-v.y());

        return;
    }

    method void show() {
        do Screen.setColor(true);
        do drawTurtle();
        return;
    }

    method void hide() {
        do Screen.setColor(false);
        do drawTurtle();
        return;
    }

    method int turn(int d) {
        do hide();
        do setDirection(direction + d);
        do show();
        return direction;
    }

    method void turnleft(int d) {
        do turn(d);
        return;
    }

    method void turnright(int d) {
        do turn(-d);
        return;
    }

    method void forward(int l) {
        var int x, y;

        do hide();

        if (_pendown) {
            do Screen.setColor(true);
        } else {
            do Screen.setColor(false);
        }

        do v.calculate(l, direction);
        let x = _x + v.x();
        let y = _y - v.y();

        do Screen.drawLine(_x, _y, x, y);

        do setX(x);
        do setY(y);

        do show();

        return;
    }

    method void penup() {
        let _pendown = false;
        return;
    }

    method void pendown() {
        let _pendown = true;
        return;
    }
}
