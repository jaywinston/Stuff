class Turtle {

    static int TLEN;
    field int direction, _x, _y;
    field Vector v;
    field ScreenArea sa;
    field boolean _pendown;

    constructor Turtle new(int x, int y, int d) {
        let TLEN = 10;
        let _x = x;
        let _y = y;
        let direction = setDirection(d);
        let v = Vector.new();
        let sa = ScreenArea.new();
        let _pendown = false;
        do show();
        return this;
    }

    method int setDirection(int d) {
        let direction = util.mod(d, 360);
        if (direction < 0) {
            let direction = direction + 360;
        }
        return direction;
    }

    method int direction() {
        return direction;
    }

    method int x() {
        return _x;
    }

    method int y() {
        return _y;
    }

    method void drawTurtle() {
        var int i, l, tlen;

        let tlen = 10;
        let i = 150;
        let l = 0;
        while (i < 180) {
            do v.calculate(tlen-(l/6), direction+i);
            do Screen.drawLine(_x, _y, _x+v.x(), _y-v.y());
            let i = i + 1;
            let l = l + 1;
        }
        do v.calculate(tlen-(l/6), direction+i);
        do Screen.drawLine(_x, _y, _x+v.x(), _y-v.y());
        while (i < 210) {
            do v.calculate(tlen-(l/6), direction+i);
            do Screen.drawLine(_x, _y, _x+v.x(), _y-v.y());
            let i = i + 1;
            let l = l - 1;
        }
        do v.calculate(tlen-(l/6), direction+i);
        do Screen.drawLine(_x, _y, _x+v.x(), _y-v.y());

        return;
    }

    method void show() {
        do sa.clear();
        //do v.calculate(TLEN, direction);
        //do sa.copy(_x-v.x(), _y-v.y(), _x+v.x(), _y+v.y());
        do sa.copy(_x-TLEN, _y-TLEN, _x+TLEN, _y+TLEN);
        do Screen.setColor(true);
        do drawTurtle();
        return;
    }

    method void hide() {
        do Screen.setColor(false);
        do drawTurtle();
        do sa.restore();
        return;
    }

    method int turn(int d) {
        do hide();
        do setDirection(direction + d);
        do show();
        return direction;
    }

    method int turnleft(int d) {
        return turn(d);
    }

    method int turnright(int d) {
        return turn(-d);
    }

    method void forward(int l) {
        var int x, y;

        do hide();

        do v.calculate(l, direction);
        let x = _x + v.x();
        let y = _y - v.y();

        if (_pendown) {
            do Screen.setColor(true);
            do Screen.drawLine(_x, _y, x, y);
        }

        let _x = x;
        let _y = y;

        do show();

        return;
    }

    method void penup() {
        let _pendown = false;
        return;
    }

    method void pendown() {
        let _pendown = true;
        return;
    }
}
