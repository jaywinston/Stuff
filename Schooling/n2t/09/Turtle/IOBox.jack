class IOBox {

    static int BUFSIZ, MAXLEN;
    static int bp, rp;
    static Array linbuf;
    static TextBox tb;


    function void init(int i, int j, int h, int w) {

        let BUFSIZ = 23;
        let MAXLEN = Str.MAXLEN();
        let linbuf = Array.new(BUFSIZ);
        let bp = 0;
        while (bp < BUFSIZ) {
            let linbuf[bp] = String.new(MAXLEN);  /* for memory diagnostics */
            let bp = bp + 1;
        }
        let bp = 0;
        let tb = TextBox.new(i, j, h, w);

        return;
    }


    function void dispose() {
        var int i;
        var String s;

        let i = 0;
        while (i < BUFSIZ) {
            let s = linbuf[i];
            do s.dispose();
            let i = i + 1;
        }
        do linbuf.dispose();
        do tb.dispose();

        return;
    }


    function String read(String ps) {
        var char c;
        var String s;

        do tb.puts(ps);
        let rp = bp;

        let s = linbuf[bp];
        do s.dispose();
        let s = String.new(MAXLEN);
        let linbuf[bp] = s;

        let c = 0;
        while (~(c = 128)) {
            do tb.putc(0);      /* show cursor */
            do tb.backspace();  /* next char overwrites cursor */

            let c = Keyboard.keyPressed();
            while (c = 0) {
                let c = Keyboard.keyPressed();
            }
            do Memory.poke(24576, 0);

            if ( (c>31) & (c<127) ) {
                if (s.length() < MAXLEN) {
                    do s.appendChar(c);
                    do tb.putc(c);
                }
            } else { if ( (c=129) & (s.length()>0) ) {
                do s.eraseLastChar();
                do tb.putc(c);
            } else { if (c = 131) {
                let s = IOBox.recall(-1);
            } else { if (c = 133) {
                let s = IOBox.recall(1);
            }}}}
        }

        if (~(rp = bp)) {
            let s = linbuf[bp];
            do s.dispose();
            let s = linbuf[rp];
            let linbuf[bp] = IOBox.strdup(s);
        }

        if (s.length() > 0) {
            let bp = bp + 1;
            if (bp = BUFSIZ) {
                let bp = 0;
            }
        }

        do Output.printChar(129);   /* erase cursor */
        do tb.newline();

        return s;
    }


    function String recall(int step) {
        var int i;
        var String s;

        /* clear current string from screen from end */
        let s = linbuf[rp];
        let i = 0;
        while (i < s.length()) {
            do tb.putc(32);
            do tb.backspace();
            do tb.backspace();
            let i = i + 1;
        }

        if ( (rp=(BUFSIZ-1)) & (step>0) ) {
            let rp = 0;
        } else { if ( (rp=0) & (step<0) ) {
            let rp = BUFSIZ - 1;
        } else {
            let rp = rp + step;
        }}

        let s = linbuf[rp];

        /* prevent stepping into unwritten buf slots, only non empty strings
           are written to the buffer so an empty string indicates unwritten */
        if ( (s.length()=0) & ~(rp=bp) ) {
            if ( (rp=(BUFSIZ-1)) & (step<0) ) {
                let rp = 0;
            } else { if ( (rp=0) & (step>0) ) {
                let rp = BUFSIZ - 1;
            } else {
                let rp = rp - step;
            }}
            let s = linbuf[rp];
        }

        do tb.puts(s);

        return s;
    }


    function void print(String s) {
        do tb.puts(s);
        return;
    }


    function void echo(String s) {
        do tb.prints(s);
        return;
    }


    function String strdup(String s) {
        var int i;
        var String t;

        let t = String.new(MAXLEN);
        let i = 0;
        while (i < s.length()) {
            do t.appendChar(s.charAt(i));
            let i = i + 1;
        }

        return t;
    }
}
