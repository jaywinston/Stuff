class Lib {


    function int mod(int n, int d) {
        var int q;

        let q = n / d;
        return n - (q*d);
    }


    function Array ERRTAB() {
        var Array errtab;

        let errtab = Array.new(2);
        let errtab[0] = null;
        let errtab[1] = "error";

        return errtab;
    }


    function Symbol copySym(Symbol sym) {
        var int type;

        if (sym = null) {
            return null;
        }

        let type = sym.meta();

        if (type = Lib.STR()) {
            return Symbol.new(Str.dup(sym.name()), type, Str.dup(sym.data()));
        }

        if (       (type=Lib.BLOCK())
                | ((type=Lib.CMDLIN()) 
                |  (type=Lib.FUNC())) ) {
            return Symbol.new(Str.dup(sym.name()), type,
                              Lib.copyList(sym.data()));
        }

        return Symbol.new(Str.dup(sym.name()), type, sym.data());
    }


    function Symbol copyList(Symbol sym) {
        var Symbol new;

        if (sym = null) {
            return null;
        }

        let new = Lib.copySym(sym);

        let sym = sym.next();
        while (~(sym = null)) {
            do new.append(Lib.copySym(sym));
            let sym = sym.next();
        }

        return new;
    }


    function void destroySym(Symbol sym) {
        var int type;
        var String s;
        var HashTable ht;

        if (sym = null) {
            return;
        }

        let s = sym.name();

        if (~(s = null)) {
            do s.dispose();
        }

        let type = sym.meta();

        if (       (type=Lib.STR())
                | ((type=Lib.END())
                |  (type=Lib.EXPR())) ) {
            let s = sym.data();
            do s.dispose();
        } else { if (   (type=Lib.BLOCK())
                     | ((type=Lib.CMDLIN())
                     |  (type=Lib.FUNC())) ) {
            do Lib.destroyList(sym.data());
        } else { if (type = Lib.HASHTAB()) {
            let ht = sym.data();
            do Lib.destroyHashTab(ht);
        }}}

        do sym.dispose();

        return;
    }


    function void destroyList(Symbol sym) {
        var Symbol next;

        while (~(sym = null)) {
            let next = sym.next();
            do Lib.destroySym(sym);
            let sym = next;
        }

        return;
    }


    function void destroyHashTab(HashTable ht) {
        var int i;

        let i = 0;
        while (i < ht.hashSize()) {
            do Lib.destroyList(ht.bucket(i));
            let i = i + 1;
        }

        do ht.dispose();

        return;
    }


    function int BLOCK     () { return  1; }
    function int CMD       () { return  2; }
    function int CMDLIN    () { return  3; }
    function int FUNC      () { return  4; }
    function int END       () { return  5; }
    function int EXPR      () { return  6; }
    function int INT       () { return  7; }
    function int STR       () { return  8; }
    function int SYM       () { return  9; }
    function int HASHTAB   () { return 10; }
}
