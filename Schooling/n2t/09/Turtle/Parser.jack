class Parser {

    field String ST_DLMS, EX_DLMS;
    field String _token, fName;
    field HashTable symtab;
    field Symbol argv;

    /* token types */
    static int CMD, CMDLIN, FUNC, INT, STR, TYPE;

    constructor Parser new() {
        let ST_DLMS = " =";
        let EX_DLMS = "+-*/%(){}";
        let _token = String.new(lib.MAXLEN());
        let fName = String.new(lib.MAXLEN());
        let symtab = HashTable.new();
        do installKWs();
        return this;
    }

    method void dispose() {
        do ST_DLMS.dispose();
        do EX_DLMS.dispose();
        do symtab.dispose();
        do Memory.deAlloc(this);
        return;
    }

    method void installKWs() {
        var int i;
        var Symbol p;

        let i = 0;
        do installKW("type",    0, i);  let i = i + 1;  // 'type' must be first
        do installKW("cmd",     0, i);  let i = i + 1;
        do installKW("cmdlin",  0, i);  let i = i + 1;
        do installKW("func",    0, i);  let i = i + 1;
        do installKW("int",     0, i);  let i = i + 1;
        do installKW("str",     0, i);

        let p = symtab.lookup("type");    let TYPE   = p.value();
        let p = symtab.lookup("cmd");     let CMD    = p.value();
        let p = symtab.lookup("cmdlin");  let CMDLIN = p.value();
        let p = symtab.lookup("func");    let FUNC   = p.value();
        let p = symtab.lookup("int");     let INT    = p.value();
        let p = symtab.lookup("str");     let STR    = p.value();

        let i = 0;
        do installKW("BLOCK",     CMD, i);  let i = i + 1;
        do installKW("CLEAR",     CMD, i);  let i = i + 1;
        do installKW("FORWARD",   CMD, i);  let i = i + 1;
        do installKW("FUNCTION",  CMD, i);  let i = i + 1;
        do installKW("PENDOWN",   CMD, i);  let i = i + 1;
        do installKW("PENUP",     CMD, i);  let i = i + 1;
        do installKW("PRINT",     CMD, i);  let i = i + 1;
        do installKW("REPEAT",    CMD, i);  let i = i + 1;
        do installKW("TURNLEFT",  CMD, i);  let i = i + 1;
        do installKW("TURNRIGHT", CMD, i);  let i = i + 1;
        do installKW("QUIT",      CMD, i);

        return;
    }

    method void installKW(String name, int type, int val) {
        do symtab.install(name, type, val);
        do name.dispose();
        return;
    }

    method int lookup(String s) {
        var Symbol p;

        let p = symtab.lookup(s);
        return p.value();
    }

    /* stub */
    method Symbol parse(String s) {
        var char op;
        var int i;
        var String name, token;

        do Str.truncate(_token);

        let i = Str.tkn(s, _token, ST_DLMS, 0);

        if (i = s.length()) {
            return Symbol.dup(symtab.lookup(_token));
        }

        let op = _token.charAt(_token.length()-1);
        do _token.eraseLastChar();

        if (op = 61) {
            let name = Str.dup(_token);
            do Str.truncate(_token);
            if (Str.tkn(s, _token, ST_DLMS, i) < s.length()) {
                do Error.set(0);  // too many arguments for assignment
                return null;
            }
            let argv = eval(_token);
            do symtab.install(name, argv.type(), argv.value());
            do argv.dispose();
            return true;  // don't like this
        }

        if (~(op = 32)) {
            do Error.set(0);  // Parser.parse(): op not ' ' or =
        }

        let argv = Symbol.dup(symtab.lookup(_token));

        if (argv = null) {
            do Error.set(0);  // Parser.parse(): sym not found
            return null;
        }

        if (~(argv.type() = CMD)) {
            do Error.set(0);  // Parser.parse(): sym not cmd
            return null;
        }

        while (i < s.length()) {
            do Str.truncate(_token);
            let i = Str.tkn(s, _token, ST_DLMS, i);
            if (_token.charAt(_token.length()-1) = 32) {
                do _token.eraseLastChar();
            }
            do argv.append(eval(_token));
        }

        return argv;
    }

    /* stub */
    method Symbol eval(String s) {
        var char c;
        var Symbol arg;

        let c = s.charAt(0);

        if (c = 34) {
            if (s.charAt(s.length()-1) = 34) {
                let arg = Symbol.new(lib.NULLS(), STR, expr(s, 0));
            } else {
                do Error.set(0);  // Parser.eval(): open string
                return null;
            }

        } else { if ( ((c>47) & (c<58)) | (((c|32)>96) & ((c|32)<123)) ) {
            /* todo: string symbols */
            let arg = Symbol.new(s, INT, expr(s, 0));

        } else {
            do Error.set(0);  // Parser.eval(): unrecognized char
            return null;
        }}

        return arg;
    }

    method int expr(String s, int i) {
        var char c, op;
        var int n, sLen;
        var String token;
        var Symbol p;

        if (s.charAt(0) = 34) {     // "
            do s.eraseLastChar();
            do Str.shift(s, 1);
            return Str.dup(s);
        }

        if (s.charAt(0) = 40) {     // (
            do Str.shift(s, 1);
        }

        let token = String.new(s.length());

        let i = Str.tkn(s, token, EX_DLMS, i);

        let c = token.charAt(0);
        if ( (c>47) & (c<58) ) {
            let n = String.intValue(token);
        } else { /* fix this whole block */
            let c = c | 32;
            if ( (c>96) & (c<127) ) {
                if (i < s.length()) {  /* fix me */
                    let op = token.charAt(token.length()-1);
                    do token.eraseLastChar();
                } else {
                    let op = 0;
                }
                let p = symtab.lookup(token);
                /* todo: type check */
                let n = p.value();
            } else {
                do Error.set(0);  // Parser.expr(): unrecognized char
                return null;
            }
        }

        if (i < s.length()) {
            if (op=0) {let op = token.charAt(token.length()-1);}
            if ( (op=42) | ((op=47) | (op=37)) ) {
                do Str.truncate(token);
                let i = Str.tkn(s, token, EX_DLMS, i);
                let n = operate(n, expr(token, 0), op);
                let op = token.charAt(token.length()-1);
            }
            if (i < s.length()) {
                let n = operate(n, expr(s, i), op);
            }
        }

        do token.dispose();

        return n;
    }

    method int operate(int m, int n, char op) {
        if (op = 43) { return m + n; }
        if (op = 45) { return m - n; }
        if (op = 42) { return m * n; }
        if (op = 47) { return m / n; }
        if (op = 37) { return lib.mod(m, n); }
        return 0;
    }
}
