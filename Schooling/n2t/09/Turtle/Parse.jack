class Parse {

    static int EQ, BLANK, DELIM, BLOCK, END, EOS, CMDLIN, INT, COMMA,
                INTEX, OP, STR, STREX, SYM, UOP, LPAREN, RPAREN;
    static int CLASSMASK;
    static String PS1, PS2;
    static Symbol ARGV;


    function void init() {
        let EQ = Lex.cclass(61);
        let BLANK = Lex.cclass(32);
        let BLOCK = LibSym.BLOCK();
        let END = Lex.cclass(125);
        let EOS = Lex.cclass(0);
        let CMDLIN = LibSym.CMDLIN();
        let INT = LibSym.INT();
        let INTEX = LibSym.INTEX();
        let OP = LibSym.OP();
        let STR = LibSym.STR();
        let STREX = LibSym.STREX();
        let COMMA = STREX & ~BLOCK;
        let SYM = LibSym.SYM();
        let UOP = LibSym.UOP();
        let LPAREN = Lex.cclass(40);
        let RPAREN = Lex.cclass(41);
        let CLASSMASK = -16;
        let PS1 = ">";
        let PS2 = "|";
        let ARGV = Symbol.new(null, null, null);
        return;
    }


    function Symbol next() {

        do ARGV.setData(null);
        do Parse.cmdlin(IOBox.read(PS1), ARGV, 0);

        return ARGV.data();
    }


    function int cclass(String s, int i) {
        return Lex.cclass(Lex.getch(s, i));
    }


    function int cmdlin(String s, Symbol argv, int i) {
        var int cclass;
        var String t;
        var Symbol extok, next;

        let i = Str.lskip(s, 32, i);
        let cclass = Parse.cclass(s, i) & CLASSMASK;
        while (~(cclass = EOS)) {

            if (cclass = END) {
                do argv.setTag(s);
                return i;
            }

            if (cclass = BLOCK) {
                let next = Symbol.new(null, BLOCK, null);
                let i = Parse.block(s, next, i);
                let s = next.tag();
                do next.setTag(null);
            } else { if (cclass = INT) {
                let t = String.new(s.length());
                let i = Lex.token(s, t, i);
                let next = Symbol.new(null, INT, t.intValue());
                do t.dispose();
            } else { if (cclass = INTEX) {
                let next = Symbol.new(null, INTEX, null);
                let i = Parse.intex(s, next, i);
                let s = next.tag();
                do next.setTag(null);
            } else { if (cclass = STR) {
                let t = String.new(s.length());
                let i = Lex.string(s, t, i);
                let next = Symbol.new(null, STR, t);
            } else { if (cclass = SYM) {
                let t = String.new(s.length());
                let i = Lex.token(s, t, i);
                /* assignment? */
                if ( (Parse.cclass(s, i) = EQ)
                        & (~(Parse.cclass(s, i+1) = EQ)) ) {
                    let next = Symbol.new("=", SYM, null);
                    do next.append(Symbol.new(t, SYM, null));
                    let i = i + 1;
                } else {
                    let next = Symbol.new(t, SYM, null);
                }
            } else {
                return -i;  // Error.set parse.cmdlin bad token (probably ',')
            }}}}}

            let cclass = Parse.cclass(s, i) & CLASSMASK;
            if (cclass = INTEX) {
                let extok = Symbol.new(null, INTEX, null);
                do extok.append(next);
                let i = Parse.intex(s, extok, i);
                let s = extok.tag();
                do extok.setTag(null);
                let next = extok;
            } else { if (cclass = STREX) {
                let extok = Symbol.new(null, STREX, null);
                do extok.append(next);
                let i = Parse.strex(s, extok, i);
                let s = extok.tag();
                do extok.setTag(null);
                let next = extok;
            }}

            do argv.append(next);

            let i = Str.lskip(s, 32, i);
            let cclass = Parse.cclass(s, i) & CLASSMASK;
        }

        do argv.setData(argv.next());
        do argv.setNext(null);
        do argv.setTag(s);

        return i;
    }


    function int intex(String s, Symbol queue, int i) {
        var boolean uop;
        var int cclass, op;
        var String t;
        var Symbol objbuf, stack, stackbuf;

        let stack = Symbol.new(null, null, null);
        let uop = queue.next() = 0;
        let cclass = Parse.cclass(s, i);

        /* todo: should END be in exit cond? */
        while ( (cclass & ((BLANK|COMMA)|(END|EOS))) = 0 ) {

            if ( (cclass=INT) | ((cclass=SYM) | (cclass=BLOCK)) ) {
                if (cclass = INT) {
                    let t = String.new(5);
                    let i = Lex.token(s, t, i);
                    let objbuf = Symbol.new(null, INT, t.intValue());
                    do t.dispose();
                } else { if (cclass = SYM) {
                    let t = String.new(5);
                    let i = Lex.token(s, t, i);
                    let objbuf = Symbol.new(t, SYM, null);
                } else { if (cclass = BLOCK) {
                    let objbuf = Symbol.new(null, BLOCK, null);
                    let i = Parse.block(s, objbuf, i);
                    let s = objbuf.tag();
                    do objbuf.setTag(null);
                }}}
                do queue.append(objbuf);
                while (stack.meta() = UOP) {
                    let stackbuf = stack;
                    let stack = stack.next();
                    do stackbuf.setNext(null);
                    do queue.append(stackbuf);
                }
                let uop = false;
            } else { if ( (cclass&OP) = OP) {
                let op = s.charAt(i);
                if (uop) {
                    let objbuf = Symbol.new(null, UOP, op);
                } else {
                    let objbuf = Symbol.new(null, Lex.cclass(op)&15, op);
                    if ( (i+1) < s.length() ) {
                        if ( (Lex.cclass(op)=EQ)
                                & (Lex.cclass(s.charAt(i+1))=EQ) ) {
                            let i = i + 1;
                            do objbuf.setData((op*100) + s.charAt(i));
                        }
                    }
                    /* pop higher equal ops to queue */
                    while ( ~(stack.meta()<objbuf.meta())
                            & ~(stack.meta()=LPAREN) ) {
                        let stackbuf = stack;
                        let stack = stack.next();
                        do stackbuf.setNext(null);
                        do queue.append(stackbuf);
                    }
                    let uop = true;
                }
                do objbuf.setNext(stack);
                let stack = objbuf;
                let i = i + 1;
            } else { if (cclass = LPAREN) {
                let objbuf = Symbol.new(null, LPAREN, null);
                do objbuf.setNext(stack);
                let stack = objbuf;
                let uop = true;
                let i = i + 1;
            } else { if (cclass = RPAREN) {
                while (~(stack.meta() = LPAREN)) {
                    if (stack.meta() = null) { /* mismatched parens */
                        do stack.dispose();
                        return -i;  // Error.set mismatched parens
                    }
                    let stackbuf = stack;
                    let stack = stack.next();
                    do stackbuf.setNext(null);
                    do queue.append(stackbuf);
                }
                let stackbuf = stack;
                let stack = stack.next();
                do stackbuf.dispose();
                let uop = false;
                let i = i + 1;
            }}}}

            let cclass = Parse.cclass(s, i);
        }

        /* pop ops to queue */
        while (~(stack.meta() = null)) {
            if (stack.meta() = LPAREN) {    /* mismatched parens */
                while (~(stack = null)) {
                    let stackbuf = stack;
                    let stack = stack.next();
                    do stackbuf.dispose();
                }
                return -i;  // Error.set mismatched parens
            }
            let stackbuf = stack;
            let stack = stack.next();
            do stackbuf.setNext(null);
            do queue.append(stackbuf);
        }

        do stack.dispose();

        do queue.setData(queue.next());
        do queue.setNext(null);
        do queue.setTag(s);

        return i;
    }


    function int strex(String s, Symbol next, int i) {
        var int cclass;
        var String t;
        var Symbol arg, intex;

        while (Parse.cclass(s, i) = STREX) {

            let i = i + 1;
            let cclass = Parse.cclass(s, i) & CLASSMASK;

            if (cclass = BLOCK) {
                let arg = Symbol.new(null, BLOCK, null);
                let i = Parse.block(s, arg, i);
                let s = arg.tag();
                do arg.setTag(null);
            } else { if (cclass = INT) {
                let t = String.new(s.length());
                let i = Lex.token(s, t, i);
                let arg = Symbol.new(null, INT, t.intValue());
                do t.dispose();
            } else { if (cclass = INTEX) {
                let arg = Symbol.new(null, INTEX, null);
                let i = Parse.intex(s, arg, i);
                let s = arg.tag();
                do arg.setTag(null);
            } else { if (cclass = STR) {
                let t = String.new(s.length());
                let i = Lex.string(s, t, i);
                let arg = Symbol.new(null, STR, t);
            } else { if (cclass = SYM) {
                let t = String.new(s.length());
                let i = Lex.token(s, t, i);
                let arg = Symbol.new(t, SYM, null);
            }}}}}

            if ( (Parse.cclass(s, i)&CLASSMASK) = INTEX) {
                let intex = Symbol.new(null, INTEX, null);
                do intex.append(arg);
                let i = Parse.intex(s, intex, i);
                let s = intex.tag();
                do intex.setTag(null);
                let arg = intex;
            }

            do next.append(arg);
        }

        do next.setData(next.next());
        do next.setNext(null);
        do next.setTag(s);

        return i;
    }


    function int block(String s, Symbol block, int i) {
        var Symbol cmdlin;

        let i = Str.lskip(s, 32, i+1);

        if ( (Parse.cclass(s, i)&(END|EOS)) = 0 ) {
            let cmdlin = Symbol.new(null, CMDLIN, null);
            let i = Parse.cmdlin(s, cmdlin, i);
            let s = cmdlin.tag();
            do cmdlin.setTag(null);
            do block.append(cmdlin);
        }

        while (Parse.cclass(s, i) = EOS) {
            let s = IOBox.read(PS2);
            let i = Str.lskip(s, 32, 0);
            if (~(Parse.cclass(s, i) = END)) {
                let cmdlin = Symbol.new(s, CMDLIN, null);
                let i = Parse.cmdlin(s, cmdlin, 0);
                let s = cmdlin.tag();
                do cmdlin.setTag(null);
                do block.append(cmdlin);
            }
        }

        do block.setData(block.next());
        do block.setNext(null);
        do block.setTag(s);

        return i + 1;
    }

}
