class Parse {

    static int EQ, EQOP, BLANK, DELIM, BLOCK, BEND, EOS, CMDLIN, INT, COMMA,
                INTEX, OP, STR, STREX, SYM, UOP, LPAREN, RPAREN, NOLGL,
                LIST, LEND;
    static int CLASSMASK;
    static String PS1, PS2;
    static Symbol ARGV;


    function void init() {

        let EQ = Lex.cclass(61);
        let EQOP = Lex.cclass(60);
        let BLANK = Lex.cclass(32);
        let BLOCK = LibSym.BLOCK();
        let BEND = Lex.cclass(125);
        let EOS = Lex.EOS();
        let CMDLIN = LibSym.CMDLIN();
        let INT = LibSym.INT();
        let INTEX = LibSym.INTEX();
        let OP = LibSym.OP();
        let STR = LibSym.STR();
        let STREX = LibSym.STREX();
        let COMMA = STREX & ~BLOCK;
        let SYM = LibSym.SYM();
        let UOP = LibSym.UOP();
        let LPAREN = Lex.cclass(40);
        let RPAREN = Lex.cclass(41);
        let NOLGL = Lex.NOLGL();
        let LIST = Lex.cclass(91);
        let LEND = Lex.cclass(93);

        let CLASSMASK = -16;

        let PS1 = ">";
        let PS2 = "|";

        let ARGV = Symbol.new(null, null, null);

        return;
    }


    function void dispose() {
        do PS1.dispose();
        do PS2.dispose();
        do LibSym.destroyList(ARGV);
        return;
    }


    function Symbol next() {

        do ARGV.setData(null);

        if (Parse.cmdlin(LibIO.read(PS1), ARGV, 0) < 0) {
            do LibSym.destroyList(ARGV.data());
            return null;
        }

        do ARGV.setTag(null);

        return ARGV.data();
    }


    function int cclass(String s, int i) {
        return Lex.cclass(Lex.getch(s, i));
    }


    function void mkpackage(Symbol pkg, String tag) {

        if (pkg = null) {
            return;
        }

        do pkg.setData(pkg.next());
        do pkg.setNext(null);
        do pkg.setTag(tag);

        return;
    }


    function int cmdlin(String s, Symbol argv, int i) {
        var int cclass;
        var String t;
        var Symbol extok, next;

/*
        if ( (i<0) | ~(i<s.length()) ) {
            return -1;
        }
*/

        let i = Str.lskip(s, 32, i);
        let cclass = Parse.cclass(s, i) & CLASSMASK;
        while (~(cclass = EOS)) {

            if (cclass = NOLGL) {
                do Parse.mkpackage(argv, s);
                return -1;
            }

            if ( (cclass=BEND) | (cclass=LEND) ) {
                do Parse.mkpackage(argv, s);
                return i;
            }

            if (cclass = LIST) {
                let next = Symbol.new(null, LIST, null);
                let i = Parse.list(s, next, i);
                let s = next.tag();
                do next.setTag(null);
            } else { if (cclass = BLOCK) {
                let next = Symbol.new(null, BLOCK, null);
                let i = Parse.block(s, next, i);
                let s = next.tag();
                do next.setTag(null);
            } else { if (cclass = INT) {
                let t = String.new(s.length());
                let i = Lex.token(s, t, i);
                let next = Symbol.new(null, INT, t.intValue());
                do t.dispose();
            } else { if (cclass = INTEX) {
                let next = Symbol.new(null, INTEX, null);
                let i = Parse.intex(s, next, i);
                let s = next.tag();
                do next.setTag(null);
            } else { if (cclass = STR) {
                let t = String.new(s.length());
                let i = Lex.string(s, t, i);
                if (i < 0) {
                    do Parse.mkpackage(argv, t);
                    do LibIO.error("EOS while scanning string");
                    return -1;
                }
                let next = Symbol.new(null, STR, t);
            } else { if (cclass = SYM) {
                let t = String.new(s.length());
                let i = Lex.token(s, t, i);
                if ( (Parse.cclass(s, i) = EQ)  /* assignment? */
                        & (~(Parse.cclass(s, i-1) = EQOP)) ) {
                    let next = Symbol.new("ASSIGN", SYM, null);
                    do next.append(Symbol.new(t, SYM, null));
                    let i = i + 1;
                } else {
                    let next = Symbol.new(t, SYM, null);
                }
            } else {
                do Parse.mkpackage(argv, s);
                do LibIO.error("illegal character");
                return -1;
            }}}}}}

/*
            if (i < 0) {
                return i;
            }
*/

            let cclass = Parse.cclass(s, i) & CLASSMASK;
            if (cclass = INTEX) {
                let extok = Symbol.new(null, INTEX, null);
                do extok.append(next);
                let i = Parse.intex(s, extok, i);
                let s = extok.tag();
                do extok.setTag(null);
                let next = extok;
            } else { if (cclass = STREX) {
                let extok = Symbol.new(null, STREX, null);
                do extok.append(next);
                let i = Parse.strex(s, extok, i);
                let s = extok.tag();
                do extok.setTag(null);
                let next = extok;
            }}
/*
            if (i < 0) {
                do Parse.mkpackage(argv, s);
                return i;
            }
*/

            do argv.append(next);

            let i = Str.lskip(s, 32, i);
            let cclass = Parse.cclass(s, i) & CLASSMASK;
        }

        do Parse.mkpackage(argv, s);

        return i;
    }


    function int intex(String s, Symbol queue, int i) {
        var boolean uop;
        var int cclass, op;
        var String t;
        var Symbol objbuf, stack, stackbuf;

        if ( (i<0) | ~(i<s.length()) ) {
            return -1;
        }

        let stack = Symbol.new(null, null, null);
        let uop = queue.next() = 0;
        let cclass = Parse.cclass(s, i);

        /* todo: should BEND be in exit cond? */
        while ( (cclass & ((BLANK|COMMA)|(BEND|EOS))) = 0 ) {

            if ( (cclass=INT) | ((cclass=SYM) | (cclass=BLOCK)) ) {
                if (cclass = INT) {
                    let t = String.new(5);
                    let i = Lex.token(s, t, i);
                    let objbuf = Symbol.new(null, INT, t.intValue());
                    do t.dispose();
                } else { if (cclass = SYM) {
                    let t = String.new(5);
                    let i = Lex.token(s, t, i);
                    let objbuf = Symbol.new(t, SYM, null);
                } else { if (cclass = BLOCK) {
                    let objbuf = Symbol.new(null, BLOCK, null);
                    let i = Parse.block(s, objbuf, i);
                    let s = objbuf.tag();
                    do objbuf.setTag(null);
                }}}

                if (i < 0) {
                    return i;
                }

                do queue.append(objbuf);
                while (stack.meta() = UOP) {
                    let stackbuf = stack;
                    let stack = stack.next();
                    do stackbuf.setNext(null);
                    do queue.append(stackbuf);
                }

                let uop = false;

            } else { if ( (cclass&OP) = OP) {
                let op = s.charAt(i);
                let i = i + 1;
                if (uop) {
                    let objbuf = Symbol.new(null, UOP, op);
                } else {
                    let objbuf = Symbol.new(null, Lex.cclass(op)&15, op);
                    if (i < s.length()) {
                        if ( ((Lex.cclass(op)=EQ) | (Lex.cclass(op)=EQOP))
                                & (Parse.cclass(s, i)=EQ) ) {
                            do objbuf.setData((op*100) + 61);
                            let i = i + 1;
                        }
                    }
                    /* pop higher/equal ops to queue */
                    while ( ~(stack.meta()<objbuf.meta())
                            & ~(stack.meta()=LPAREN) ) {
                        let stackbuf = stack;
                        let stack = stack.next();
                        do stackbuf.setNext(null);
                        do queue.append(stackbuf);
                    }
                    let uop = true;
                }
                do objbuf.setNext(stack);
                let stack = objbuf;
            } else { if (cclass = LPAREN) {
                let objbuf = Symbol.new(null, LPAREN, null);
                do objbuf.setNext(stack);
                let stack = objbuf;
                let uop = true;
                let i = i + 1;
            } else { if (cclass = RPAREN) {
                while (~(stack.meta() = LPAREN)) {
                    if (stack.meta() = null) { /* mismatched parens */
                        do stack.dispose();
                        do LibIO.error("mismatched parentheses");
                        return -1;
                    }
                    let stackbuf = stack;
                    let stack = stack.next();
                    do stackbuf.setNext(null);
                    do queue.append(stackbuf);
                }
                let stackbuf = stack;
                let stack = stack.next();
                do stackbuf.dispose();
                let uop = false;
                let i = i + 1;
            }}}}

            let cclass = Parse.cclass(s, i);
        }

        /* pop ops to queue */
        while (~(stack.meta() = null)) {
            if (stack.meta() = LPAREN) {    /* mismatched parens */
                while (~(stack = null)) {
                    let stackbuf = stack;
                    let stack = stack.next();
                    do stackbuf.dispose();
                }
                do LibIO.error("mismatched parentheses");
                return -1;
            }
            let stackbuf = stack;
            let stack = stack.next();
            do stackbuf.setNext(null);
            do queue.append(stackbuf);
        }

        do stack.dispose();

        do Parse.mkpackage(queue, s);

        return i;
    }


    function int strex(String s, Symbol next, int i) {
        var int cclass;
        var String t;
        var Symbol arg, intex;

        if ( (s=null) | (next=null) ) {
            return -1;
        }

        let arg = next.next();
        if (arg = null) {
            return -1;
        }

        if (~(arg.meta() = STR)) {
            do LibIO.error("no format string given");
            return -1;
        }

        let arg = null;

        if ( (i<0) | ~(i<s.length()) ) {
            return -1;
        }

        while (Parse.cclass(s, i) = STREX) {

            let i = i + 1;
            let cclass = Parse.cclass(s, i) & CLASSMASK;

            if (cclass = BLOCK) {
                let arg = Symbol.new(null, BLOCK, null);
                let i = Parse.block(s, arg, i);
                let s = arg.tag();
                do arg.setTag(null);
            } else { if (cclass = INT) {
                let t = String.new(s.length());
                let i = Lex.token(s, t, i);
                let arg = Symbol.new(null, INT, t.intValue());
                do t.dispose();
            } else { if (cclass = INTEX) {
                let arg = Symbol.new(null, INTEX, null);
                let i = Parse.intex(s, arg, i);
                let s = arg.tag();
                do arg.setTag(null);
            } else { if (cclass = STR) {
                let t = String.new(s.length());
                let i = Lex.string(s, t, i);
                if (i < 0) {
                    do Parse.mkpackage(next, t);
                    do LibIO.error("EOS while scanning string");
                    return -1;
                }
                let arg = Symbol.new(null, STR, t);
            } else { if (cclass = SYM) {
                let t = String.new(s.length());
                let i = Lex.token(s, t, i);
                let arg = Symbol.new(t, SYM, null);
            }}}}}

            if (i < 0) {
                return i;
            }

            if ( (Parse.cclass(s, i)&CLASSMASK) = INTEX) {
                let intex = Symbol.new(null, INTEX, null);
                do intex.append(arg);
                let i = Parse.intex(s, intex, i);
                let s = intex.tag();
                do intex.setTag(null);
                let arg = intex;
            }

            if (i < 0) {
                return i;
            }

            do next.append(arg);
        }

        do Parse.mkpackage(next, s);

        return i;
    }


    function int block(String s, Symbol block, int i) {
        var Symbol cmdlin;

        if ( (i<0) | ~(i<s.length()) ) {
            return -1;
        }

        let i = Str.lskip(s, 32, i+1);
        if ( (Parse.cclass(s, i)&(BEND|EOS)) = 0 ) {
            let cmdlin = Symbol.new(null, CMDLIN, null);
            let i = Parse.cmdlin(s, cmdlin, i);
            let s = cmdlin.tag();
            do cmdlin.setTag(null);
            do block.append(cmdlin);
        }

        if (i < 0) {
            return i;
        }

        while (Parse.cclass(s, i) = EOS) {
            let s = LibIO.read(PS2);
            let i = Str.lskip(s, 32, 0);
            if (~(Parse.cclass(s, i) = BEND)) {
                let cmdlin = Symbol.new(s, CMDLIN, null);
                let i = Parse.cmdlin(s, cmdlin, 0);
                let s = cmdlin.tag();
                do cmdlin.setTag(null);
                if (i < 0) {
                    do Parse.mkpackage(cmdlin, s);
                    return i;
                }
                do block.append(cmdlin);
            }
        }

        do Parse.mkpackage(block, s);

        return i + 1;
    }


    /* temporary! */
    function int list(String s, Symbol next, int i) {
        var Collection list;
        var Symbol p,q;

        let list = Collection.new();
        let i = Parse.cmdlin(s, next, i+1);
        let s = next.tag();
        do next.setTag(null);
        let p = next.data();
        while (~(p = null)) {
            let q = p.next();
            do p.setNext(null);
            do list.append(p);
            let p = q;
        }
        do next.setNext(list);
        do Parse.mkpackage(next, s);

        return i;
    }

}
