class LibSym {


    function Symbol copySym(Symbol sym) {
        var int typeClass;

        if (sym = null) {
            return null;
        }

        let typeClass = LibSym.typeClass(sym);

        if (typeClass = LibSym.STR()) {
            return Symbol.new(Str.dup(sym.tag()), sym.meta(),
                              Str.dup(sym.data()));
        }

        if (typeClass = LibSym.BLOCK()) {
            return Symbol.new(Str.dup(sym.tag()), sym.meta(),
                              LibSym.copyList(sym.data()));
        }

        return Symbol.new(Str.dup(sym.tag()), sym.meta(), sym.data());
    }


    function Symbol copyList(Symbol sym) {
        var Symbol new;

        if (sym = null) {
            return null;
        }

        let new = LibSym.copySym(sym);

        let sym = sym.next();
        while (~(sym = null)) {
            do new.append(LibSym.copySym(sym));
            let sym = sym.next();
        }

        return new;
    }


    function void destroySym(Symbol sym) {
        var int typeClass;
        var String s;

        if (sym = null) {
            return;
        }

        let s = sym.tag();

        if (~(s = null)) {
            do s.dispose();
        }

        let typeClass = LibSym.typeClass(sym);

        if (typeClass = LibSym.STR()) {
            let s = sym.data();
            do s.dispose();
        } else { if (typeClass = LibSym.BLOCK()) {
            do LibSym.destroyList(sym.data());
        }}

        do sym.dispose();

        return;
    }


    function void destroyList(Symbol sym) {
        var Symbol next;

        while (~(sym = null)) {
            let next = sym.next();
            do LibSym.destroySym(sym);
            let sym = next;
        }

        return;
    }


    function void destroyHashTable(HashTable ht) {
        var int i;

        let i = 0;
        while (i < ht.hashSize()) {
            do LibSym.destroyList(ht.bucket(i));
            let i = i + 1;
        }

        do ht.dispose();

        return;
    }


    function void install(HashTable ht, String name, int type, int value) {
        var String s;
        var Symbol p;

        let p = ht.lookup(name);

        if (p = null) {
            do ht.install(Str.dup(name), type, value);
        } else {
            if (p.meta() = LibSym.STR()) {
                let s = p.data();
                do s.dispose();
            } else { if (p.meta() = LibSym.FUNC()) {
                do LibSym.destroyList(p.data());
            }}
            do p.setMeta(type);
            do p.setData(value);
        }


        return;
    }


    function int typeClass(Symbol sym) {
        return sym.meta() & 96;
    }


    /* Don't change these values without consulting typeClass(). */

    function int INT       () { return  Lex.cclass(48); }
    function int CMD       () { return  2; }
    function int HASHTAB   () { return  3; }
    function int OP        () { return  4; }
    function int SYM       () { return  5; }
    function int TTL       () { return  6; }

    /* string type-class */
    function int STR       () { return Lex.cclass(34); }
    function int END       () { return 33; }
    function int EXPR      () { return 34; }

    /* block type-class */
    function int BLOCK     () { return Lex.cclass(123);     }
    function int CMDLIN    () { return Lex.cclass(123) + 1; }
    function int FUNC      () { return Lex.cclass(123) + 2; }
    function int INTEX     () { return Lex.cclass(42);      }
    function int STREX     () { return Lex.cclass(44);      }

}
