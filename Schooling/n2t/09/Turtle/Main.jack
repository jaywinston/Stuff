class Main {

    static boolean running;
static boolean mv;  // ##DB##
    static int INT, LIST, STR;
    static Array FARGV;
    static String FSSTR, FDSTR;


    function void main() {
        do Main.printBanner("Turtle 0.01");
        do Main.init();
        do Main.run();
        do Main.killall();
        return;
    }


    function void printBanner(String banner) {
        do Output.moveCursor(0, 23);  /* magic number!s */
        do Output.printString(banner);
        do banner.dispose();
        do Screen.setColor(true);
        do Screen.drawLine(0, 13, 509, 13);  /* magic number!s */
        return;
    }


    function void init() {
let mv=false;  // ##DB##

        do Vector.init();
        do Turtle.init(2);
        do Collection.init();
        do LibIO.init();
        do LibSym.init();
        do Lex.init(
            LibSym.ctab(),
            34,  /* magic number! */
            92,  /* magic number! */
            LibIO.esctab(),
            LibSym.CHARSETSIZE(),
            LibSym.CHARSETSIZE()+1);
        do Parse.init();
        do Eval.init();
        do Bin.init();
        do Exec.init();

        let INT = LibSym.INT();
        let LIST = LibSym.LIST();
        let STR = LibSym.STR();
        let FARGV = Array.new(1);
        let FSSTR = "'%S'";
        let FDSTR = "%D";

        return;
    }


    function void killall() {  // todo: this
        do FARGV.dispose();
        do FSSTR.dispose();
        do FDSTR.dispose();
        return;
    }


    function void run() {
        var Collection argv;

        let running = true;

        while (running) {
            let argv = Parse.next();
            do Main.try(argv);
if(mv){let mv=DB.dbi(DB.mc());}  // ##DB##
            do LibSym.destroyList(argv);
        }

        return;
    }


    function void try(Collection argv) {
        var String s;

        if (argv = null) {
            return;
        }

        if (LibIO.noerr()) {
            do Main.execute(argv);
        }

        /* Error messages are queued. */
        do LibIO.clearerr();

        while (LibIO.isqueued()) {
            let s = LibIO.get();
            do LibIO.echo(s);
            do s.dispose();
        }

        return;
    }


    function void execute(Collection argv) {
        var Symbol ret;

        while (argv.length() > 0) {
            let ret = Exec.cmdlin(argv);
            do LibIO.fixCanvas();
            if (LibIO.noerr()) {
                do Main.enqueue(ret);
            } else {
                do LibSym.destroyList(argv);
                let argv = Collection.new();
            }
            do LibSym.destroySym(ret);
        }

        return;
    }


    function void enqueue(Symbol sym) {

        if (sym = null) {
            return;
        }

let mv=sym.meta()=1&sym.data()=128;  // ##DB##

        let FARGV[0] = sym.data();
        if (sym.meta() = STR) {
            do LibIO.put(Str.format(FSSTR, FARGV));
        } else { if (sym.meta() = INT) {
            do LibIO.put(Str.format(FDSTR, FARGV));
        } else { if (sym.meta() = LIST) {
            do LibIO.put(LibSym.formatList(FARGV[0]));
        }}}

        return;
    }


    function void stop() {
        let running = false;
        return;
    }

}
