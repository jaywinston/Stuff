class Main {

    static boolean running;
    static int INT, STR;
    static Array FARGV;
    static String FSSTR, FDSTR;


    function void main() {

        do Main.printBanner();

        do Main.init();

        let INT = LibSym.INT();
        let STR = LibSym.STR();
        let FARGV = Array.new(1);
        let FSSTR = "'%S'";
        let FDSTR = "%D";

        do Main.run();

        do Main.killall();

        do FARGV.dispose();
        do FSSTR.dispose();
        do FDSTR.dispose();

        return;
    }


    function void printBanner() {
        var String banner;

        let banner = "Turtle 0.01";
        do Output.moveCursor(0, 23);
        do Output.printString(banner);
        do banner.dispose();
        do Screen.setColor(true);
        do Screen.drawLine(0, 13, 509, 13);

        return;
    }


    function void init() {
        do Vector.init();
        do Turtle.init(2);
        do Collection.init();
        do LibIO.init();
        do LibSym.init();
        do Lex.init(
            LibSym.ctab(),
            34,
            92,
            LibIO.esctab(),
            LibSym.CHARSETSIZE(),
            LibSym.CHARSETSIZE()+1);
        do Parse.init();
        do Eval.init();
        do Bin.init();
        do Exec.init();
        return;
    }


    function void killall() {
        return;
    }


    function void run() {
        var Symbol argv;

        let running = true;

        while (running) {
            let argv = Parse.next();
            //while (argv.length() > 0) {
                do Main.try(argv);
            //}
            do argv.dispose();
        }

        return;
    }


    function void try(Collection argv) {
        var String s;

        if (LibIO.noerr()) {  /* This is the check for parse errors. */
            do LibSym.destroySym(
                Main.enqueue(
                    Exec.cmdlin(argv)));
            do LibIO.fixCanvas();
        }

        /* Error messages are queued. */
        do LibIO.clearerr();

        while (LibIO.isqueued()) {
            let s = LibIO.get();
            do LibIO.echo(s);
            do s.dispose();
        }

        return;
    }


    function Symbol enqueue(Symbol sym) {

        if (sym = null) {
            return null;
        }

        if (LibIO.noerr()) {
            let FARGV[0] = sym.data();
            if (sym.meta() = STR) {
                do LibIO.put(Str.format(FSSTR, FARGV));
            } else { if (sym.meta() = INT) {
                do LibIO.put(Str.format(FDSTR, FARGV));
            }}
        }

        return sym;
    }


    function void quit() {
        let running = false;
        return;
    }

}
