class Main {

    static boolean running;


    function void main() {
        var String banner;

        let banner = "Turtle 0.01";
        do Output.moveCursor(0, 23);
        do Output.printString(banner);
        do banner.dispose();
        do Screen.setColor(true);
        do Screen.drawLine(0, 13, 509, 13);

        do Vector.init();
        do Collection.init();
        do LibIO.init();
        do Lex.init();
        do LibSym.init();
        do Parse.init();
        do Eval.init();
        do Exec.init(2, 32, 20, 31);

        do Main.run();

        // todo: disposes

        return;
    }


    function void run() {
        var Symbol argv;

        let running = true;

        while (running) {
            let argv = Parse.next();
            do Main.try(argv);
            do LibSym.destroySymbol(argv);
        }

        return;
    }


    function void try(Symbol argv) {
        var String s;
        var Symbol ret;

        if (LibIO.noerr()) {  /* This is the check for parse errors. */
            let ret = Exec.exec(argv);
            do LibIO.fixBorders();
            if (LibIO.noerr()) {
                do Exec.enqueue(ret);
            }
            do LibSym.destroyList(ret);
        }

        /* Error messages are queued. */
        do LibIO.clearerr();

        while (LibIO.isqueued()) {
            let s = LibIO.get();
            do LibIO.echo(s);
            do s.dispose();
        }

        return;
    }


    function void stop() {
        let running = false;
        return;
    }

}
