class ScreenArea {

    field int aLen, height, width;
    field Array words;
    static Array orig;

    /** w and h are in pixels. */
    constructor ScreenArea new(int w, int h) {
        var int i;

        let width = (w/16) + 1;
        let height = h;
        let aLen = width * height;
        let orig = 16384;
        let words = Array.new(aLen);
        let i = 0;
        while (i < aLen) {
            let words[i] = Word.new(0, 0);
            let i = i + 1;
        }

        return this;
    }

    method void dispose() {
        var int i;
        var Word w;

        let i = 0;
        while (i < aLen) {
            let w = words[i];
            do w.dispose();
            let i = i + 1;
        }
        do words.dispose();
        do Memory.deAlloc(this);

        return;
    }

    method void clear() {
        return;
    }

    /** copy aLen Words(), starting from (x, y) */
    method int copy(int x, int y) {
        var int i;
        var int h, w;
        var int base, row;
        var Word word;

        let base = (x/16) + (y*32);
        let h = 0;
        let i = 0;
        while (h < height) {
            let row = base + (h*32);
            let w = 0;
            while (w < width) {
                let word = words[i];
                do word.setAddress(orig+(row+w));
                do word.setValue(orig[row+w]);
                let i = i + 1;
                let w = w + 1;
            }
            let h = h + 1;
        }

        return i;
    }

    method int restore() {
        var int n;
        var Array a;
        var Word w;

        let a = 0;
        let n = 0;
        while (n < aLen) {
            let w = words[n];
            let a[w.address()] = w.value();
            let n = n + 1;
        }

        return n;
    }

}
