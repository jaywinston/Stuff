class Console {

    static int INT, STR, SYM;
    static Terminal term;
    static Canvas canvas;

    function void init() {
        var String banner, nulls;

        let banner = "                       Turtle 0.01";

        do Screen.drawLine(0, 13, 509, 13);
        do Output.moveCursor(0, 0);
        do Output.printString(banner);
        do banner.dispose();

        let INT = Const.INT();
        let STR = Const.STR();
        let SYM = Const.SYM();
        let term = Terminal.new(2, 1, 20, 30);

        do Console.try(null);

        return;
    }

    function void run() {
        while (true) {
            do Console.try(Canvas.exec(term.read()));
        }
        return;
    }

    function void try(Symbol retVal) {

        if (Error.isSet()) {
            do term.echo(Error.message());
            if (Error.isSet()) {
                do term.echo(Error.message());
            }
        } else {
            if (~(retVal = null)) {
                do Console.echo(retVal);
            }
        }

        do term.print(Canvas.ready(retVal));

        return;
    }

    function void echo(Symbol sym) {
        while (~(sym = null)) {
            if (sym.type() = STR) {
                do term.echo(sym.value());
            } else { if (sym.type() = SYM) {
                do Console.echo(sym.value());
            } else { if (sym.type() = INT) {
                do Console.echoInt(sym);
            }}}
            let sym = sym.next();
        }
        return;
    }

    function void echoInt(Symbol sym) {
        var String s;

        let s = String.new(6);
        do s.setInt(sym.value());
        do term.echo(s);
        do s.dispose();
        return;
    }

    function String read() {
        return term.read();
    }
}

