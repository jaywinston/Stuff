class Console {

    static int INT, STR, SYM;
    static String PS1;
    static Terminal term;
    static Canvas canvas;

    function void init() {
        var String banner;

        let banner = "Turtle 0.01";
        do Output.moveCursor(0, 23);
        do Output.printString(banner);
        do banner.dispose();
        do Screen.setColor(true);
        do Screen.drawLine(0, 13, 509, 13);

        let INT = Const.INT();
        let STR = Const.STR();
        let SYM = Const.SYM();
        let PS1 = ">";

        do Console.run();

        return;
    }

    function void run() {
        while (true) {
            do Console.try(Canvas.exec(Terminal.read(PS1)));
        }
        return;
    }

    function void try(Symbol retVal) {

        if (Error.isSet()) {
            do Terminal.echo(Error.message());
            if (Error.isSet()) {
                do Terminal.echo(Error.message());
            }
        } else {
            do Console.echo(retVal);
        }

        return;
    }

    function void echo(Symbol sym) {
        while (~(sym = null)) {
            if (sym.type() = STR) {
                do Terminal.echo(sym.value());
            } else { if (sym.type() = SYM) {
                do Console.echo(sym.value());
            } else { if (sym.type() = INT) {
                do Console.echoInt(sym);
            }}}
            let sym = sym.next();
        }
        return;
    }

    function void echoInt(Symbol sym) {
        var String s;

        let s = String.new(6);
        do s.setInt(sym.value());
        do Terminal.echo(s);
        do s.dispose();
        return;
    }

    function String read(String ps) {
        return Terminal.read(ps);
    }
}

