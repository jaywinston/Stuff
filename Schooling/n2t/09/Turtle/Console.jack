class Console {

    field int STR, SYM;
    field Terminal term;
    field Canvas canvas;

    constructor Console new() {
        var String banner, nulls;

        let banner = "                       Turtle 0.01";
        let nulls = "";

        do Screen.drawLine(0, 13, 509, 13);
        do Output.moveCursor(0, 0);
        do Output.printString(banner);
        do banner.dispose();

        let STR = Const.STR();
        let SYM = Const.SYM();
        let term = Terminal.new(2, 1, 20, 30);

        do try(null);
        do nulls.dispose();

        return this;
    }

    method void run() {
        while (true) {
            do try(Canvas.exec(term.read()));
        }
        return;
    }

    method void try(Symbol retVal) {

        if (Error.isSet()) {
            do term.echo(Error.message());
            if (Error.isSet()) {
                do term.echo(Error.message());
            }
        } else {
            if (~(retVal = null)) {
                do echo(retVal);
            }
        }

        do term.print(Canvas.ready(retVal));

        return;
    }

    method void echo(Symbol sym) {
        while (~(sym = null)) {
            if (sym.type() = STR) {
                do term.echo(sym.value());
            } else { if (sym.type() = SYM) {
                do echo(sym.value());
            } else {
                do echoInt(sym);
            }}
            let sym = sym.next();
        }
        return;
    }

    method void echoInt(Symbol sym) {
        var String s;

        let s = String.new(5);
        do s.setInt(sym.value());
        do term.echo(s);
        do s.dispose();
        return;
    }

}

