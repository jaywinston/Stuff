// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Memory.jack

/**
 * This library provides two services: direct access to the computer's main
 * memory (RAM), and allocation and recycling of memory blocks. The Hack RAM
 * consists of 32,768 words, each holding a 16-bit binary number.
 */ 
class Memory {

    static int base, cap;
    static int SIZE, NEXT;
    static Array memory, free;

    /** Initializes the class. */
    function void init() {
        let memory = 0;
        let SIZE = 0;  // index of block's size
        let NEXT = 1;    // index of block's next
        let free = 2048;  // base
        let free[SIZE] = 14334;
        let free[NEXT] = null;
        return;
    }

    /** Returns the RAM value at the given address. */
    function int peek(int address) {
        return memory[address];
    }

    /** Sets the RAM value at the given address to the given value. */
    function void poke(int address, int value) {
        let memory[address] = value;
        return;
    }

    /** Finds an available RAM block of the given size and returns
     *  a reference to its base address. */
    function int alloc(int size) {
        var Array p, q;

        if (size < 1) {
            do Sys.error(5);
        }
        if (size > 14334) {
            do Sys.error(6);
        }

        let p = free;
        let q = p;

        while ( ~(p[NEXT]=null) & (p[SIZE]<size) ) {
            let q = p;
            let p = p[NEXT];
        }

        if (p[SIZE] = size) {
            let q[NEXT] = p[NEXT];
        } else {
            let p[SIZE] = p[SIZE] - (size+2);
            let p = p + (p[SIZE]+2);
            let p[SIZE] = size;
        }

        return p + 2;
    }

    /** De-allocates the given object (cast as an array) by making
     *  it available for future allocations. */
    function void deAlloc(Array o) {
        var Array p, q;

        let o = o - 2;

        let p = free;
        while ( ~(p[NEXT]=null) & (p[NEXT]<o) ) {
            let p = p[NEXT];
        }
        let q = p[NEXT];

        if ( ((o+o[SIZE])+2) = q ) {
            let o[SIZE] = (o[SIZE]+q[SIZE]) + 2;
            let o[NEXT] = q[NEXT];
        } else {
            let o[NEXT] = q;
        }
            
        if ( ((p+p[SIZE])+2) = o ) {
            let p[SIZE] = (p[SIZE]+o[SIZE]) + 2;
            let p[NEXT] = o[NEXT];
        } else {
            let p[NEXT] = o;
        }

        return;
    }    
}
